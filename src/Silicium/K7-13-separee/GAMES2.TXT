
0 '*****************
1 '** OLYMPIC ******
2 '*******GAMES 2 **
3 '*****************
4 'CANON X-07 + 4 Ko
10 KEY$(1)="    "
11 KEY$(2)="ouiç"
12 KEY$(3)="nonè"
13 KEY$(4)="    "
14 KEY$(5)="    "
15 KEY$(6)="    "
16 KEY$(7)="    "
17 KEY$(8)="    "
18 KEY$(9)="    "
19 KEY$(10)="    "
20 KEY$(11)="    "
21 KEY$(12)="    "
22 CONSOLE0,4,0,0,0:CONSOLEà1,1:CLEAR200
23 FONT$(129)="&h00,&h10,&h30,&h30,&h18,&h18,&h30,&h20"
24 FONT$(130)="&h00,&h10,&h30,&h58,&h10,&h18,&h28,&h40"
25 FONT$(131)="&h00,&h10,&h38,&h54,&h10,&h18,&h64,&h00"
26 FONT$(132)="&h00,&h10,&h30,&h58,&h10,&h50,&h28,&h04"
27 FONT$(133)="&h00,&h10,&h30,&h38,&h10,&h30,&h18,&h08"
28 FONT$(134)="&hFC,&h48,&hFC,&hA8,&h54,&hA8,&hFC,&h00"
29 FONT$(135)="&h00,&hFC,&hA8,&h54,&hA8,&h54,&hFC,&h00"
30 FONT$(136)="&h00,&h10,&h3C,&h18,&h10,&h10,&h30,&h10"
31 FONT$(137)="&h00,&h10,&h3C,&h38,&h10,&h18,&h30,&h20"
32 FONT$(138)="&h00,&h10,&h3C,&h58,&h10,&h18,&h28,&h40"
33 FONT$(139)="&h00,&h10,&h3C,&h58,&h10,&h18,&h64,&h00"
34 FONT$(140)="&h00,&h10,&h3C,&h58,&h10,&h50,&h28,&h04"
35 FONT$(141)="&h00,&h10,&h3C,&h38,&h10,&h30,&h18,&h08"
36 FONT$(142)="&h00,&h00,&hC0,&h00,&h00,&h00,&h00,&h00"
37 FONT$(224)="&h3C,&h40,&h98,&hA0,&hA0,&h98,&h40,&h3C"
39 FONT$(128)="&h00,&h10,&h10,&h10,&h10,&h10,&h30,&h10"
40 CLS
41 FORI=1TO30STEP3
42 CIRCLE(I*2,16),I
43 CIRCLE(119-(I*2),16),I
44 NEXTI
45 FORI=1TO30
46 CIRCLE(60,16),I
47 NEXTI
48 A$="      David DER BAGHDASSARIAN prsente OLYMPIC GAMES 2    "
49 LOCATE6,2:PRINT"        "
50 FORI=35TO83:PRESET(I,15):NEXTI
51 FORI=15TO23:PRESET(35,I):NEXTI
52 FORI=1TOLEN(A$)-2
53 X$=MID$(A$,I,8)
54 LOCATE6,2:PRINTX$
55 FORK=0TO5:NEXTK
56 NEXT I
57 FORI=0TO250:NEXTI
58 CLS:FORI=1TO3:CIRCLE(10*I,10),7:NEXTI:CIRCLESTEP(-5,8),7
59 CIRCLESTEP(-10,0),7:LOCATE8,1:PRINT"OLYMPIC":LOCATE11,2:PRINT"GAMES 2"
60 LINE(0,0)-(119,0):LINE-(119,31):LINE-(0,31):LINE-(0,0)
61 BL=18:NO=9:NP=13:CR=4:BP=27
62 BEEP13,NO:BEEP18,NO:BEEP18,CR:BEEP20,CR:BEEP22,NO:BEEP18,NO:BEEP25,BL
63 BEEP22,NP:BEEP22,CR:BEEP23,NO:BEEP25,CR:BEEP23,CR:BEEP22,CR:BEEP23,CR
64 BEEP25,NO:BEEP20,CR:BEEP18,CR:BEEP20,CR:BEEP22,CR:BEEP20,NO:BEEP0,CR
65 BEEP13,NO:BEEP18,NO:BEEP18,CR:BEEP20,CR:BEEP22,NO:BEEP18,NO
66 BEEP25,BL:BEEP22,NP:BEEP22,CR:BEEP23,CR:BEEP25,CR:BEEP22,CR:BEEP23,CR
67 BEEP20,NP:BEEP18,CR:BEEP18,BP
68 '****************
69 '*** TE  DEUM ***
70 '****************
71 FORI=0TO250:NEXTI
72 CLS:PRINT"Explication °O/N§ ?":CONSOLE0,4,1,0,0
73 A$=INKEY$
74 IFA$="è"THEN102
75 IFA$<>"ç"THEN73
76 A$="HYPER-OLYMPICS 2 estune version identiq-ue au clbre jeu   d'arcade.  "
77 GOSUB95
78 A$="Comme pour ce derni-er , vous devez par-ticiper a plusieurs preuves . "
79 GOSUB95
80 A$="Sur l'cran du CANONvous tes reprsent- par un athlte .             "
81 GOSUB95
82 A$="Pour faire courrir  l'athlte , il faut appuyer sur toutes  les touches"
83 GOSUB95
84 A$="sauf sur la barre   d'espacement .Pour  faire augmenter     l'angle il "
85 GOSUB95
86 A$="faut laisser la bar-re d'espacement app-uye .Choisissez un bon angle  "
87 GOSUB95
88 A$="et le tour est jou!                                                   "
89 GOSUB95
90 A$="Si vous voulez avoirle maximum de pointsil faudra se qualif-ier a      "
91 GOSUB95
92 A$="toutes les preuves.                           BONNE CHANCE            "
93 GOSUB95
94 A$="    àá  David DER       BAGHDASSARIAN       et Le CLUB C7           1985
95 CLS:CONSOLE0,4,0,0,0:FORI=1TOLEN(A$)
96 IFMID$(A$,I,1)=" "THENB=0ELSEB=5
97 BEEPB,1:PRINTMID$(A$,I,1);
98 NEXTI
99 A$=INKEY$:IFA$=CHR$(13)THEN102ELSEIFA$=""THEN99
100 ONERRORGOTO334
101 CLS:RETURN
102 ONERRORGOTO334
103 CONSOLE0,4,0,0,0:IFINKEY$<>""THEN103ELSEFORI=0TO250:NEXTI
104 INIT£1,"RECORD",50,"D":INPUT£1,HS,HN$:IFLEN(HN$)>15THENHN$=LEFT$(HN$,15)
105 CLS:INPUT"Prnom ";V$
106 CLS
107 R=1:SC=0:RC=14:RS=7.5:RJ=80
108 CLS:LOCATE0,0:PRINT"ROUND No";R
109 MA=0:ML=0:TE=0
110 FORI=0TO3
111 BEEPI*I,1:BEEPI*I*I,1
112 NEXTI
113 FORI=0TO250:NEXTI
114 '***************
115 '***************
116 '**100  METRES**
117 '***************
118 '***************
119 CLS:FORI=0TO31
120 LINE(I,I)-(0,0):LINE(0,31)-(I,I):LINE(I,I)-(31,31):LINE(31,0)-(I,I)
121 NEXTI
122 LOCATE7,1:PRINT"100 Mtres"
123 FORI=0TO1STEP2
124 FORJ=102TO41STEP-1:BEEP410+(1020-J*10),1
125 PSET(J,17)
126 NEXTJ
127 FORJ=102TO41STEP-1:BEEPJ*10,1
128 PRESET(J,17)
129 NEXTJ
130 NEXTI
131 CLS
132 FORI=0TO19:PRINTCHR$(134);:NEXTI
133 PRINT"Q:";RC;"s"
134 FORI=0TO19:PRINTCHR$(135);:NEXTI
135 PRINTCHR$(128);:LOCATE17,3:PRINT"F";
136 X=0:IFINKEY$=""THEN136ELSETIME$="00:00:00":GOTO137
137 FORI=0TO15
138 FORJ=128TO133
139 IFINKEY$=""THEN139ELSEX=X+.167
140 LOCATEX,3:PRINT" ";CHR$(J);
141 NEXTJ
142 NEXTI
143 TE$=TIME$:TE$=RIGHT$(TE$,2)
144 TE=VAL(TE$):TE=100*TE:TE=INT(TE):TE=TE/100
145 LOCATE0,1:PRINT"T:";TE;"s       ":FORI=0TO250:NEXTI
146 FORI=32TO0STEP-2
147 BEEPI,1
148 NEXTI
149 IFTE>RCTHENGOSUB329:GOTO273
150 GOSUB324
151 CLS:FORI=1TO3:CIRCLE(I*10,10),7:NEXTI:CIRCLESTEP(-5,8),7
152 CIRCLESTEP(-10,0),7:SC=SC+((RC-TE)*300)
153 LOCATE11,1:PRINT"Score";:LOCATE11,2:PRINTSC;:FORI=0TO500:NEXTI
154 FORI=0TO500:NEXTI
155 FORI=60TO49STEP-1:BEEPI,1:NEXTI
156 FORI=490TO500:BEEPI,1:NEXTI
157 BEEP31,10
158 IFINKEY$<>""THEN158
159 '***************
160 '***************
161 '* SAUT EN *****
162 '**** LONGUEUR *
163 '***************
164 '***************
165 CLS:FORI=0TO31
166 LINE(I,I)-(0,0):LINE(0,31)-(I,I):LINE(I,I)-(31,31):LINE(31,0)-(I,I)
167 NEXTI
168 LOCATE11,1:PRINT"Saut en":LOCATE11,2:PRINT"Longueur"
169 FORI=0TO1STEP2
170 FORJ=114TO64STEP-1:BEEP410+(1170-J*10),1
171 PSET(J,25)
172 NEXTJ
173 FORK=114TO64STEP-1:BEEPK*10,1
174 PRESET(K,25)
175 NEXTK
176 NEXTI
177 CLS:ES=1
178 CLS:X=0:LOCATE6,1:PRINT"Essai No";ES:FORI=0TO500:NEXTI:CLS
179 FORI=0TO19:PRINTCHR$(134);:NEXTI:PRINT"Q:";RS;"m"
180 FORI=0TO19:PRINTCHR$(135);:NEXTI
181 LOCATE0,3:PRINTCHR$(128);:X=0
182 IFINKEY$=""THEN182ELSEGOTO183
183 FONT$(143)="&h00,&h00,&h00,&h00,&h00,&h00,&h00,&h40"
184 LOCATE11,3:PRINTCHR$(143);
185 TIME$="00:00:00"
186 FORI=0TO9:FORJ=128TO133:LOCATEX,3:PRINT" ";CHR$(J);:X=X+.167
187 IFINKEY$=""THEN187
188 NEXTJ,I:BEEP9,1
189 BEEP18,1:FORI=0TO8:A=STRIG(0):IFA=-1THEN191ELSENEXTI
190 GOTO211
191 AN=1:TE$=RIGHT$(TIME$,2):TE=VAL(TE$):V=INT(X/(2*TE)*850)
192 IFTKEY(" ")=0THEN196
193 AN=AN+3:LOCATE15,1:PRINTAN;CHR$(223);
194 IFAN>=90THEN196
195 GOTO192
196 L=5.2-ABS(43-AN)*0.105+(V/249):L=L*100:L=INT(L):L=L/100
197 CIRCLE(X*6+L*2+6,35),L*2:LOCATE0,1:PRINT"L:";L;"m      "
198 FORI=0TO500:NEXTI
199 ES=ES+1:IFL>MLTHENML=L
200 IFL>=RSTHENGOSUB324:GOTO202
201 GOSUB329
202 '
203 '
204 IFES<4THEN178ELSESC=SC+INT(ML*275):IFML<RSTHEN273
205 CLS:FORI=1TO3:CIRCLE(I*10,10),7:NEXTI:CIRCLESTEP(-5,8),7
206 CIRCLESTEP(-10,0),7:LOCATE11,1:PRINT"Score";:LOCATE11,2:PRINTSC;
208 FORI=0TO500:NEXTI:FORI=60TO49STEP-1:BEEPI,1:NEXTI:FORI=490TO500
209 BEEPI,1:NEXTI:BEEP31,10:FORI=0TO1000:IFINKEY$<>""THENNEXTELSE213
210 '
211 FORI=0TO5:LOCATE0,1:PRINT"FOUL !!!!!":BEEPI,3:NEXTI
212 L=0:GOTO199
213 '***************
214 '***************
215 '* LANCER DU ***
216 '**** JAVELOT **
217 '***************
218 '***************
219 CLS:FORI=0TO31
220 LINE(I,I)-(0,0):LINE(0,31)-(I,I):LINE(I,I)-(31,31):LINE(31,0)-(I,I)
221 NEXTI
222 LOCATE10,2:PRINT"Javelot"
223 FORI=0TO1STEP2
224 FORJ=103TO58STEP-1:BEEP410+(1030-J*10),1
225 PSET(J,25)
226 NEXTJ
227 FORJ=103TO58STEP-1:BEEPJ*10,1
228 PRESET(J,25)
229 NEXTJ
230 NEXTI
231 ES=1:X=0
232 CLS:LOCATE6,1:PRINT"Essai No";ES:FORI=0TO500:NEXTI:CLS:X=0
233 FORI=0TO19:PRINTCHR$(134);:NEXTI:PRINT"Q:";RJ;"m"
234 FORI=0TO19:PRINTCHR$(135);:NEXTI
235 LOCATE0,3:PRINTCHR$(136)+CHR$(142);:LOCATE10,3:PRINTCHR$(143);
236 FORI=1TO6:JA$(I)=CHR$(135+I)+CHR$(142):NEXTI
237 IFINKEY$=""THEN237ELSETIME$="00:00:00"
238 FORI=0TO8:FORJ=1TO6:LOCATEX,3:PRINT" ";JA$(J);
239 IFINKEY$=""THEN239
240 X=X+.167:NEXTJ:NEXTI
241 BEEP9,1:BEEP18,1
242 FORI=0TO8:A=STRIG(0):IFA=-1THEN244ELSENEXTI
243 GOTO271
244 AN=1:TE$=RIGHT$(TIME$,2):TE=VAL(TE$):V=INT(X/(2*TE)*990)
245 IFTKEY(" ")=0THEN249
246 AN=AN+3:LOCATE15,1:PRINTAN;CHR$(223);
247 IFAN>=90THEN249
248 GOTO245
249 L=(5.5-ABS(43-AN)*0.105+(V/249))*10
250 L=L*100:L=INT(L):L=L/100
251 FORI=(X*6)+6TO((X*6)+6)+(L/10)*5:BEEP1,1
252 PSET(I,26):PSET(I+1,26):PSET(I+2,26):PSET(I+3,26)
253 PSET(I+4,26)
254 PRESET(I,26):PRESET(I+1,26):PRESET(I+2,26)
255 PRESET(I+3,26):PRESET(I+4,26)
256 NEXTI
257 PSET(I+4,27):PSET(I+5,28):PSET(I+6,29)
258 PSET(I+7,30):PSET(I+8,31)
259 LOCATE0,1:PRINT"L:";L;"m      ";:FORI=0TO500:NEXTI
260 ES=ES+1:IFL>=MATHENMA=L
261 IFL>=RJTHENGOSUB324:GOTO263
262 GOSUB329
263 IFES<4THEN232
264 IFL>=RJTHENC=SC+1000
265 SC=SC+INT(L*24.9):IFL<RJTHEN273
266 CLS:FORI=1TO3:CIRCLE(I*10,10),7:NEXTI:CIRCLESTEP(-5,8),7
267 CIRCLESTEP(-10,0),7
268 LOCATE11,1:PRINT"Score";:LOCATE11,2:PRINTSC;:FORI=0TO500:NEXTI
269 CLS
270 GOTO273
271 FORI=0TO5:LOCATE0,1:PRINT"FOUL !!!!!":BEEPI,3:NEXTI
272 L=0:GOTO260
273 CLS:LOCATE0,0:PRINT"Le ROUND est fini...";
274 BL=18:NO=9:CR=4:NP=13:BP=27:DC=2
275 BEEP15,DC:BEEP15,CR*1.5:BEEP15,DC:BEEP20,NO:BEEP20,NO:BEEP22,NO
276 BEEP22,NO:BEEP27,NP:BEEP24,CR:BEEP20,CR:BEEP0,DC:BEEP20,DC
277 BEEP24,CR*1.5:BEEP20,DC:BEEP17,NO:BEEP25,BL
278 BEEP22,CR*1.5:BEEP19,DC:BEEP20,BP+CR:BEEP0,CR:BEEP20,CR*1.5:BEEP22,DC
279 BEEP24,NO:BEEP24,NO:BEEP24,NO:BEEP25,CR*1.5:BEEP24,DC:BEEP24,NO
280 BEEP22,NP:BEEP0,CR:BEEP22,CR*1.5:BEEP24,DC:BEEP25,NO:BEEP25,NO
281 BEEP25,NO:BEEP27,CR*1.5:BEEP25,DC:BEEP24,CR+BL:BEEP0,CR:BEEP27,CR*1.5
282 BEEP27,DC:BEEP27,NO:BEEP24,CR*1.5:BEEP20,DC:BEEP27,NO:BEEP24,CR*1.5
283 BEEP20,DC:BEEP15,CR+BL:BEEP0,DC:BEEP15,DC:BEEP15,CR*1.5:BEEP19,DC
284 BEEP22,BL:BEEP25,NO:BEEP22,CR*1.5:BEEP19,DC:BEEP20,BP
285 '***************
286 'LA MARSEILLAISE
287 '***************
288 IFMA>=RJTHENR=R+1:RC=RC-1.5:RS=RS+.5:RJ=RJ+5ELSEGOTO290
289 GOTO108
290 CLS:FORI=1TO3:CIRCLE(I*10,10),7:NEXTI:CIRCLESTEP(-5,8),7
291 CIRCLESTEP(-10,0),7
292 LOCATE11,1:PRINT"Score";
293 LOCATE11,2:PRINTSC;
294 FORI=60TO49STEP-1:BEEPI,1:NEXTI
295 FORI=490TO500:BEEPI,1:NEXTI
296 BEEP31,10
297 IFINKEY$<>""THEN297
298 IFSC>HSTHENHS=SC:HN$=V$:INIT£1,"RECORD":PRINT£1,HS,HN$
299 INIT£1,"RECORD":INPUT£1,HS,HN$
300 CLS:LOCATE0,0:PRINT"******HI-SCORE******";:LOCATE0,1:PRINT"Avec";HS;
301 PRINT"pts":LOCATE0,2:PRINTHN$
302 FORI=0TO500:NEXTI
303 CLS:CONSOLE0,4,1,0,0
304 PRINT"Une autre partie        °O/N§ ?"
305 Z$=INKEY$
306 IFZ$="ç"THENGOTO102
307 IFZ$="è"THEN309
308 GOTO305
309 CLS:BL=18:NO=9:NP=13:CR=4:BP=27
310 BEEP8,NP:BEEP10,CR:BEEP8,NO:BEEP5,BP:BEEP8,NP:BEEP10,CR:BEEP8,NO
311 BEEP5,BP:BEEP15,BL:BEEP15,NO:BEEP12,BP:BEEP13,BL:BEEP13,NO:BEEP8,BP
312 BEEP10,BL:BEEP10,NO:BEEP13,NP:BEEP12,CR:BEEP10,NO:BEEP8,NP:BEEP10,CR
313 BEEP8,NO:BEEP5,BP:BEEP10,BL:BEEP10,NO:BEEP13,NP:BEEP12,CR:BEEP10,NO
314 BEEP8,NP:BEEP10,CR:BEEP8,NO:BEEP5,BP:BEEP15,BL:BEEP15,NO:BEEP18,NP
315 BEEP15,CR:BEEP12,NO:BEEP13,BP:BEEP17,BP:BEEP13,NP:BEEP8,CR:BEEP5,NO
316 BEEP8,NP:BEEP6,CR:BEEP3,NO:BEEP1,NP+BL
317 '***************
318 '**DOUCE  NUIT**
319 '***************
320 CONSOLE0,4,0,0,0
321 CLS:LOCATE8,1:PRINT"ADIEU!";:CONSOLEà,0
322 FORI=0TO500:NEXTI
323 OFF
324 NO=9:CR=4:NP=13
325 BEEP12,NO:BEEP12,NO:BEEP13,NO:BEEP15,NO:BEEP15,NO:BEEP13,NO
326 BEEP12,NO:BEEP10,NO:BEEP8,NO:BEEP8,NO:BEEP10,NO:BEEP12,NO:BEEP12,NP
327 BEEP10,CR:BEEP10,BL
328 RETURN
329 FORI=0TO10
330 BEEPI*100,1:NEXTI
331 FORI=10TO0STEP-1
332 BEEPI*100,1:NEXTI
333 RETURN
334 FSET80:INIT£1,"RECORD",50,"D":PRINT£1,HS,HN$
335 IFLEN(HN$)>19THENHN$=LEFT$(HN$,19):A=ERL:TS=1
336 GOTO103
