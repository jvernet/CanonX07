
5 ' *** SAUTE-MOUTONS ***
10 CLS
20 FONT$(134)="16,24,144,240,240,144,144,0"
30 FONT$(135)="64,192,72,120,120,72,72,0"
40 LOCATE3,0:PRINT"Saute moutons";
50 B$=CHR$(134):P$=CHR$(135):V$=".":SS$=P$+P$+P$+V$+B$+B$+B$
60 FORI=1TO3:A$(I)=B$:A$(I+4)=P$:NEXT:A$(4)=V$
70 LOCATE6,2:FORI=1TO7STEP2:PRINTUSING"£";I;:PRINT" ";:NEXT
80 GOSUB300
90 LOCATE0,3:PRINT"Choix";
100 R$=INKEY$:IFR$=""THEN100
110 N=INSTR("12345678",R$):IFN=0THEN310
120 IFN=8THENRUN
130 PRINTN;
140 C=C+1
150 IFA$(N)=V$THEN310
160 IFA$(N)=P$THEN260
170 IFN=7THEN310
180 IFA$(N+1)=V$THENZ=N+1:GOTO210
190 IFN=6THEN310ELSEIFA$(N+2)<>V$THEN310
200 Z=N+2
210 A$(Z)=A$(N):A$(N)=V$
220 S$="":FORI=1TO7:S$=S$+A$(I):NEXT
230 IFS$<>SS$THEN80
240 GOSUB300:LOCATE0,3:PRINT"Gagne en";C;"coups";
250 R$=INKEY$:IFR$=""THEN250ELSEEND
260 IFN=1THEN310
270 IFA$(N-1)=V$THENZ=N-1:GOTO210
280 IFN=2THEN310ELSEIFA$(N-2)<>V$THEN310
290 Z=N-2:GOTO210
300 LOCATE6,1:FORI=1TO7:PRINTA$(I);:NEXT:LOCATE0,3:PRINT"       ";:RETURN
310 LOCATE0,3:PRINT"Erreur   ";:BEEP20,1:FORI=1TO150:NEXT
320 LOCATE0,3:PRINT"      ";:GOTO90
