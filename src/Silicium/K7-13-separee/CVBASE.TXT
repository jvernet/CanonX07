
100 CLS:CLEAR 100
105 A$(1)="Hexa":A$(2)="Deci":A$(3)="Octa":A$(4)="Bina"
110 CONSOLE 0,3:LOCATE 0,3:PRINT"HexaDeciOctaBin"
120 CLS:PRINT"<<<< CONVERSION >>>>";
130 LOCATE 0,1:LINE INPUT"Etat Initial:";E$:IF E$="F" GOTO 180
140 GOSUB 200:IF I=5THEN 130 ELSE D=I:Y=2:AD$=AR$
150 LOCATE 0,Y:LINE INPUT"Etat Final  :";E$:IF E$="F" THEN 180
160 GOSUB 200:IF I=5THEN Y=1:GOTO 150 :ELSE G=I
165 IF I=D THEN CLS:PRINT:AF$="BASE IDENTIQUE":LOCATE 3,1:GOSUB 2000:GOTO 120
170 CONSOLE 0,4:CLS:CONSOLE 1,3:GOSUB 500:GOTO 110
180 CONSOLE 0,4:CLS:AF$="A LA PROCHAINE SALUT":GOSUB 2000:CLS:END
200 FOR I=1TO4:IF LEFT$(A$(I),1)=E$ THEN220
210 NEXT I:GOTO 230
220 AR$=A$(I)
230 RETURN
500 PRINTUSING" <&  &>-<&  &> ";AD$;AR$
510 PRINTUSING"&  &:";AD$;:LINE INPUT H$:IF H$="" THEN RETURN
520 IF D=1 THEN 540
530 VT=VAL(H$):IF STR$(VT)<>" "+H$ THEN 650 ELSE IF D=2 THEN550 ELSE 580
540 M=1:VT=0:GOSUB 3000:IF V$="*" THEN 650 ELSE IF G=2 THEN 660
550 IF G=1 THEN V$=" "+HEX$(VT):GOTO 660
560 IF G=3 THEN B=8 ELSE IF G=4 THEN B=2
570 M=1:Z=0:F=10:GOSUB 3100:IF Z=0 THEN650 ELSE 640
580 F=ABS(D=3)*8+ABS(D=4)*2
590 B=10:M=1:Z=0:GOSUB 3100
600 IF G=1 THEN V$=" "+HEX$(Z):GOTO 660
610 IF G=2 THEN 640
620 B=ABS(F=8)*2+ABS(F=2)*8
630 VT=Z:M=1:Z=0:F=10:GOSUB 3100
640 V$=STR$(Z):GOTO 660
650 V$="*****"
660 LOCATE POS(1)+10,CSRLIN-1:PRINTAR$;":"RIGHT$(V$,LEN(V$)-1)
670 GOTO 510
2000 FOR T=1 TO LEN(AF$)
2010 BEEP 25,1:PRINTMID$(AF$,T,1);
2020 FOR T2=1 TO 10:NEXT T2,T
2030 FOR T2=1 TO 100:NEXT:RETURN
3000 FOR I=LEN(H$) TO 1 STEP -1:K=ASC(MID$(H$,I,1))
3010 IF K>64 AND K<71 THEN V=K-55:GOTO 3030
3020 IF K>47 AND K<58 THEN V=K-48 ELSE V$="*":RETURN
3030 VT=VT+V*M:M=M*16:NEXT I:V$=STR$(VT):RETURN
3100 Z=Z+(((VT/B)-INT(VT/B))*B)*M:VT=INT(VT/B)
3110 IF VT<>0 THEN M=M*F:GOTO 3100
3120 RETURN
