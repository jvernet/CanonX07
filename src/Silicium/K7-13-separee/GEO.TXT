
5 REM "GEOMETRIE"
7 REM "DEFINITION DES CARACTERES"
8 CLEAR:CLS:CONSOLE0,4,0,1,0:CONSOLEà1,1,0:FONT$(128)="0,56,124,124,124,56,0,0"
9 FONT$(129)="0,0,120,120,120,120,0,0":FONT$(130)="0,0,252,252,252,252,0,0"
10 FONT$(131)="0,32,112,248,112,32,0,0":FONT$(132)="0,0,48,120,252,252,0,0"
11 FONT$(133)="0,0,32,112,248,0,0,0":FONT$(140)="0,32,32,32,168,112,32,0"
12 REM "PRESENTATION"
13 U=RND(0):FORI=1TOINT(RND(1)*40+10):LOCATEINT(RND(1)*20),INT(RND(1)*4)
14 PRINTCHR$(128+INT(RND(1)*6)):BEEPINT(RND(1)*4096),2:NEXT
15 LOCATE6,1:PRINT"GEOMETRIE":FORI=1TO40:BEEPI,2:BEEP40-I,2:NEXT
17 REM "CHOIX DU NIVEAU"
18 CLS:PRINT"Votre Niveau de Jeu:(1) (2) (3) (4) (5)" :U=RND(0)
20 BEEP50,1:G$=INKEY$:IFG$>"0"ANDG$<"6"THENN=VAL(G$):GOTO30ELSE20
30 S=0:C=0:CLS:LOCATE5,2:PRINT"... Pret ?":GOSUB400:CLS
32 LOCATE0,0:PRINT"*** Score :";S
33 REM "BOUCLE DE JEU"
35 FORX=1TO10:E=0:FORK=2TO17STEP3:LOCATEK,3:E=E+1: PRINTCHR$(127+E);
38 REM "AFFICHAGE DES CARACTERES"
40 BEEPE*9,2:NEXTK: LOCATEC,2:PRINTCHR$(140);
50 M$="":FORJ=1TO6:M$=M$+CHR$(128+INT(RND(1)*6)):LOCATE3*J-1,1
55 PRINTRIGHT$(M$,1);:NEXTJ
60 FORK=1TON*200:NEXTK:FORZ=1TO10:BEEPZ,3:NEXTZ:LOCATE2,1:PRINTSTRING$(16," ");
65 FORM=1TO6
66 REM "ACTIONS DU JOUEUR"
70 W=0:G=C:L=STICK(0):IFL=3THENC=C+1:W=1:BEEP5,2:IFC>19THENC=19
71 IFL=7THENC=C-1:BEEP5,2:W=1:IFC<0THENC=0
72 F=SCREEN(C,3):IFW=1THENLOCATEG,2:PRINT" ";:LOCATEC,2:PRINTCHR$(140);
74 IFSTRIG(0)ANDF<>32THEN80
75 S=S+1:LOCATE11,0:BEEP1,1:PRINTS;:GOTO70
80 LOCATEC,3:PRINT" ";:BEEP9,5:LOCATE3*M-1,1:PRINTCHR$(F);:LOCATEC,3:PRINTCHR$(F);
81 IFF<>ASC(MID$(M$,M,1))THEN100ELSENEXTM:S=S-50
83 LOCATE11,0:PRINTS;:LOCATE0,3:BEEP9,20:PRINTSTRING$(19," ");:NEXTX:CLS
84 REM "FIN DU JEU"
85 FORK=1TO100STEP5:BEEPK,2:NEXT:LOCATE3,1:PRINT"... Termine !!"
86 LINE(18,17)-(100,17):FORL=100TO1STEP-10:BEEPL,9:NEXT:CLS:P(N)=P(N)+1
87 PRINT"***** NIVEAU";N;"*****";:IFP(N)=1THENR(N)=SELSEIFS<R(N)THENR(N)=S
88 REM "RESULTATS"
90 LOCATE4,1:PRINT"Score  :";S:LOCATE4,2:PRINT"Record :";R(N);:GOSUB400
95 CLS:BEEP9,6:PRINT"Une autre Partie ...  (O)ui ou (N)on ?"
96 G$=INKEY$:IFG$="O"THEN18
97 IFG$="N"THENCLS:PRINT"A bientot ...":BEEP99,9:END
98 GOTO96
99 REM "FAUX !!"
100 FORH=900TO910:BEEPH,3:NEXTH:S=S+10: LOCATE3*M-1,1:PRINT" ";:GOTO70
300 REM "BOUCLE D'ATTENTE"
400 FORZ=1TO4096:BEEPZ,1:IFINKEY$=""THENNEXT:GOTO400ELSERETURN
