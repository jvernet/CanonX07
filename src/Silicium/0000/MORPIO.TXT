
1 CLS:BEEP9,4:PRINT" Grille sur X-710 ?"
2 G$=INKEY$:IFG$="N"THEN25
3 IFG$<>"O"THEN2
4 LPRINTCHR$(18):CLEAR500:LPRINT"M40,0":LPRINT"I"
5 FORI=0TO-360STEP-80
6 LPRINT"D0,"+STR$(I)+",400,"+STR$(I)+",400,"+STR$(I-40)+",0,"+STR$(I-40)
7 NEXT:LPRINT"D0,-360,0,-400,400,-400":LPRINT"H":FORI=0TO360STEP80
8 LPRINT"D"+STR$(I)+",0,"+STR$(I)+",-400,"+STR$(I+40)+",-400,"+STR$(I+40)+",0"
9 NEXTI:LPRINT"D360,0,400,0,400,-400":FORI=20TO400STEP40
10 LPRINT"M"+STR$(I-20)+",20":LPRINT"P"+STR$((I-20)/40):NEXTI
11 FORI=-20TO-400STEP-40
12 LPRINT"M-30,"+STR$(I):LPRINT"P"+STR$((I+20)/-40):NEXTI:LPRINT"M0,-600"
25 GOSUB100
35 GOSUB600:GOSUB700
45 BEEP9,3:PRINT"Voulez-vous jouer une autre partie ?":GOSUB9000
50 IFR$="O"THEN35
55 GOSUB900:END
100 CLS:FORI=5TO114STEP3:BEEPI,2:CIRCLE(I,5),2:CIRCLE(I,26),2:NEXT:LOCATE0,1
110 PRINT"*    MORPION      * *     10*10       *"
120 DEFINTA,C-P,W-Z:DIMA(99),B(99),V(21)
260 FORJ=0TO21:V(J)=0:NEXT:FORJ=0TO4:READV(J):NEXT
330 DATA.01,.03,.5,10,10000
340 FORJ=5TO20STEP5:READV(J):NEXT
380 DATA.1,2,100,1000000:R=0:RETURN
600 FORJ=0TO99:A(J)=0:B(J)=0:NEXT:N=0:F=0:CLS:RETURN
700 BEEP1,3: PRINT"Voulez-vous entamer?":GOSUB9000
730 IFR$="O"THEN770
740 GOSUB1000:IFF<>0THEN800
770 GOSUB2000:IFF=0THEN740
800 IFF=1THENPRINT"J'ai gagne en";ELSEIFF=-1THENPRINT"Vous avez gagne en";
835 IFF=2THENPRINT"Partie nulle apres";
840 BEEP9,5: PRINTN;"cps":FORZA=1TO900:NEXT:RETURN
900 FORI=1TO20:BEEPI,2:NEXT:CLS:PRINT"Au revoir....":RETURN
1000 FORJ=0TO99:B(J)=0:NEXT:BEEP9,5:PRINT"Je joue...";
1090 C=1:J1=0:J2=5:K1=0:K2=90:GOSUB1500
1100 C=9:J1=4:J2=9:K1=0:K2=50:GOSUB1500
1110 C=10:J1=0:J2=9:K1=0:K2=50:GOSUB1500
1120 C=11:J1=0:J2=5:K1=0:K2=50:GOSUB1500
1170 S=-1:Q=0:FORJ=0TO99:IFA(J)<>0ORB(J)<STHEN1230
1220 IFB(J)>STHENC=J:S=B(C):Q=1ELSEQ=Q+1:IFQ*RND(R)<1THENC=J
1230 NEXTJ:GOSUB8000:BEEP9,3:PRINT"en";C
1250 A(C)=5:N=N+1:IFN=100OR(S=0ANDN>1)THENF=2
1290 IFS>=V(20)THENF=1
1300 RETURN
1500 C4=4*C:FORJ=J1TOJ2:FORK=K1TOK2STEP10:A=J+K:B=A+C4:Z=0
1560 FORP=ATOBSTEPC:Z=Z+A(P):NEXT
1580 Q=V(Z)
1590 IFQ<>0THENFORP=ATOBSTEPC:B(P)=B(P)+Q:NEXT
1600 NEXTK,J:RETURN
2000 BEEP9,5:INPUT"Votre coup ";A
2030 IFA<0ORA>99ORA<>INT(A)THENPRINT"un entier entre 1 et 99 svp...";:GOTO2000
2040 IFA(A)<>0THENPRINT"Case deja occupee...";:GOTO2000
2050 A(A)=1:N=N+1:IFN=100THENF=2
2110 IFB(A)>=V(4)THENF=-1
2200 RETURN
8000 BEEP14,2:BEEP16,2:BEEP22,4:BEEP22,4:BEEP15,8
8100 RETURN
9000 R$=INKEY$:IFR$=""THEN9000
9010 IFR$<>"O"ANDR$<>"N"THENPRINT"Oui ou Non ";:GOTO9000
9020 RETURN
