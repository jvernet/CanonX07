
1 '(c) LOGI-STICK 84
3 CONSOLE0,4,0,1,0:CLEAR0:CLEAR(FRE(0)-999)/14*11:ONERRORGOTO270
99 CLS:BEEP99,1:PRINT"-MENU1: (R)ESULTATS  (G)RAPHES (S)LEEP   (F)IN";
111 A$=INKEY$:IFA$=CHR$(13)THEN1000ELSEIFA$="R"ANDP+R<>0THEN2000
112 IFA$="G"THEN3000ELSEIFA$="F"THENCLS:ENDELSEIFA$<>"S"THEN111ELSESLEEP:GOTO99
200 ONERRORGOTO270:DIMA$(R,P),B$(R,P):CONSOLE0,4,0,1,0:RETURN
270 BEEP150,2:CLS:LOCATE4,1:PRINT"ERREUR No";ERR
271 IFINKEY$=""THEN271ELSERESUME99
400 PRINT£1,R,P:FORE=0TOR:FORF=0TOP:PRINT£1,A$(E,F),B$(E,F):NEXTF:NEXTE
401 BEEP99,3:GOTO99
410 INPUT£1,R,P:GOSUB200:FORE=0TOR:FORF=0TOP
411 INPUT£1,A$(E,F),B$(E,F):NEXTF:NEXTE:BEEP99,3:GOTO99
1000 BEEP99,1:CLS
1010 A$=INKEY$:IFA$="C"THEN1100ELSEIFA$="S"THEN1400ELSEIFA$=CHR$(13)THEN99
1012 IFA$="L"THEN1700ELSEIFA$="D"THEN1250ELSEIFA$="E"THEN1300ELSE1010
1100 BEEP99,1:CLS:D$="":INPUT"GROUPE:";D$:IFLEN(D$)<>1THEN1100
1101 BEEP99,1:CLS:INPUT"No DE DONNEE:";F:E=ASC(D$)-65
1120 IFF>=1ANDF<=PANDE>=0ANDE<=RTHEN1130ELSE1100
1130 BEEP99,1:CLS:PRINTA$(E,F),"DONNEE:":INPUTA$(E,F)
1131 IFVAL(A$(E,F))<0ORLEN(A$(E,F))>16THEN1130
1150 BEEP99,1:CLS:PRINTB$(E,F),"LABEL:":INPUTB$(E,F):IFLEN(B$(E,F))>16THEN1150
1160 BEEP99,1:CLS:PRINTA$(E,0),"LBL DE GROUPE":INPUTA$(E,0)
1170 IFLEN(A$(E,0))>16THEN1160ELSE99
1250 BEEP99,1:CLS:DIR
1251 IFINKEY$=""THEN1251ELSE99
1300 BEEP99,1:CLS:INPUT"LBL ";C$
1301 DELETEC$,"D":GOTO99
1400 BEEP99,1:CLS:PRINT"-SAUVEGARDE:         (K)7        (C)ARTE"
1401 A$=INKEY$:IFA$=""THEN1401ELSEIFA$="K"THEN1410ELSEIFA$="C"THEN1500ELSE1000
1410 BEEP99,1:CLS:INIT£1,"CASO:",1200,"B":GOTO400
1500 BEEP99,1:CLS:C$="":INPUT"LBL ";C$:INIT£1,C$,10+(R+1)*(P+1)*25:GOTO400
1700 BEEP99,1:CLS:PRINT"-LECTURE: (C)LAVIER   C(A)RTE (K)7"
1710 A$=INKEY$:IFA$=""THEN1710ELSEIFA$="K"THEN1750ELSEIFA$="C"THEN1850
1720 IFA$="A"THEN1800ELSE99
1750 CLEAR:BEEP99,1:CLS:INIT£1,"CASI:",1200,"B":GOTO410
1800 CLEAR:ONERRORGOTO270:BEEP99,1:CLS:INPUT"LBL ";C$
1810 INIT£1,C$:GOTO410
1850 CLEAR:BEEP99,1:CLS:PRINT"FORMAT (";INT((FRE(0)-999)/6);")"
1851 INPUT"Nb de groupes ";R:INPUT"Nb de donnees ";P
1860 IFR<1ORR>26ORP<3ORP>40ORR*P>(FRE(0)-950)/6THEN1850
1870 R=R-1:GOSUB200:FORE=0TOR
1874 BEEP99,1:CLS
1875 PRINT"LABEL DU GROUPE ";CHR$(65+E):INPUTA$(E,0):IFLEN(A$(E,0))>16THEN1874
1877 FORF=1TOP
1880 BEEP99,1:CLS:PRINTCHR$(65+E);F;" DONNEE":INPUTA$(E,F)
1890 IFVAL(A$(E,F))<0ORLEN(A$(E,F))>16THEN1880
1900 BEEP99,1:PRINT"LABEL":INPUTB$(E,F):IFLEN(B$(E,F))>16THEN1900
1910 NEXTF:NEXTE:GOTO99
2000 LPRINTCHR$(18):LPRINT"R0,-105":LPRINT"I":LPRINT"S0":LPRINT"C0":LPRINT"Q1"
2005 GOSUB5000:KK=0:LL=0:II=0:FORE=G2TOG3:FORF=1TOP:KK=KK+VAL(A$(E,F))
2010 LL=LL+1:NEXTF:NEXTE:DE=KK/LL:FORE=G2TOG3:FORF=1TOP
2015 II=II+((VAL(A$(E,F))-DE)^2)/LL:NEXTF:NEXTE:JJ=SQR(II):OO=100*JJ/DE
2020 LPRINT"M400,25":LPRINT"P*RESULTATS STATISTIQUES*"
2025 LPRINT"M320,20":LPRINT"PMOYENNE:";DE:LPRINT"M240,20":LPRINT"PVARIANCE:";II
2030 LPRINT"M160,20":LPRINT"PECART TYPE:";JJ
2040 LPRINT"M80,20":LPRINT"PCOEF. DE VAR.:";OO:LPRINT"M0,-420":GOTO99
3010 A$=INKEY$:IFA$="3"THEN3100ELSEIFA$="2"THEN3197ELSEIFA$=CHR$(13)THEN99
3020 IFA$="C"THEN3400ELSEIFA$="L"THEN3198ELSEIFA$="E"THEN3950ELSE3010
3050 LPRINTCHR$(18):LPRINT"R0,-165":LPRINT"C0":LPRINT"S0":LPRINT"I":LPRINT"Q1"
3051 C$=""
3052 CLS:PRINT"TITRE DU GRAPHE ":INPUTC$:GOSUB5000:IFXY=1THENRETURN
3054 BEEP99,1:CLS:PRINT"(V)OTRE ECHELLE     (P)ROGRAMME ECHELLE"
3055 A$=INKEY$:IFA$<>"V"ANDA$<>"P"THEN3055ELSEWW=0:FORE=G2TOG3:FORF=1TOP
3057 IFWW<VAL(A$(E,F))THENWW=VAL(A$(E,F))
3058 NEXTF:NEXTE:IFA$="P"THEN3064
3060 BEEP99,1:CLS:INPUT"ECHELLE ";VV:IFVV<WWTHEN3060
3063 GOTO3068
3064 VV=WW
3068 HH=0:BEEP99,1:CLS:GOTO8000
3070 LPRINT"D210,0,430,-430,220,-430,0,0"
3080 IFHH=0THENRETURN
3085 LPRINT"L3":FORXX=17.5TO192.5STEP17.5:LPRINT"M"+STR$(XX)+",0"
3086 LPRINT"D"+STR$(XX+220)+",-430":NEXTXX:LPRINT"L0":RETURN
3092 LPRINT"M0,-480":LPRINT"I":GOSUB3070:U1=1:GOSUB3095:LPRINT"M0,-600"
3093 GOTO3000
3095 IFHH=0THENRETURN
3096 FORXX=0TO210STEP17.5:CC=(VV*XX)/210
3097 IFU1=0THENLPRINT"M"+STR$(XX)+","+STR$(LEN(STR$(CC))*6+20):GOTO3099
3098 LPRINT"M"+STR$(220+XX)+",-440"
3099 LPRINT"P";CC:NEXTXX:RETURN
3100 IFP>12THEN3000
3105 XY=0:GOSUB3050:GOSUB3070:U1=0:GOSUB3095
3120 GOSUB3800:LPRINT"M445,-435":LPRINT"P* ";C$;" *":FORE=G2TOG3:FORF=1TOP
3130 IFF<>1THEN3137
3135 LPRINT"M0,-480":LPRINT"I":GOSUB3800:XX=0:YY=0:X2=-1:ZZ=0:X1=0:LPRINT"C0"
3136 LPRINT"M410,0":LPRINT"P";A$(E,0):LPRINT"M0,0":LPRINT"D0,-33"
3137 GOSUB3890:X1=VAL(A$(E,F))/(VV/210)+XX
3145 IFX2>=X1THEN3155
3147 LPRINT"M"+STR$(X2)+","+STR$(YY-33):LPRINT"D"+STR$(X1)+","+STR$(YY-33)
3150 LPRINT"D"+STR$(X1)+","+STR$(YY)+","+STR$(X2)+","+STR$(YY)
3155 LPRINT"M"+STR$(XX)+","+STR$(YY-33):LPRINT"D"+STR$(XX+17.5)+","+STR$(YY-66)
3157 LPRINT"D"+STR$(X1+17.5)+","+STR$(YY-66)+","+STR$(X1)+","+STR$(YY-33)
3160 LPRINT"M"+STR$(X1+17.5)+","+STR$(YY-66)
3162 LPRINT"D"+STR$(X1+17.5)+","+STR$(YY-33)
3165 IFX2<=X1THENLPRINT"D"+STR$(X1)+","+STR$(YY):GOTO3190
3170 IFX2<X1+17.5THENLPRINT"D"+STR$(X2)+","+STR$(YY-(33/(17.5/(X2-X1))))
3190 LPRINT"M"+STR$(XX+3)+","+STR$(YY-65)
3195 LPRINT"P";CHR$(E+65);RIGHT$(STR$(F),2)
3196 X2=X1+17.5:XX=XX+17.5:YY=YY-33:NEXTF:LPRINT"C0":NEXTE:GOTO3092
3197 U1=0:GOTO3200
3198 U1=1:Z=0
3200 IFP>24THEN3000
3201 XY=0:GOSUB3050:LPRINT"M470,-5":LPRINT"P* ";C$;" *":LPRINT"M450,0"
3202 LPRINT"D20,0"
3205 IFHH=0THEN3210
3206 CC=0:FORXX=0TO12:CC=(VV/12)*XX
3207 LPRINT"M"+STR$(XX*34.166+20)+","+STR$(LEN(STR$(CC))*6+20):LPRINT"P";CC
3208 NEXTXX
3210 LPRINT"M20,0":FORE=G2TOG3:FORF=1TOP:IFF<>1THEN3235
3220 LPRINT"I":GOSUB3820:YY=-40:LPRINT"M415,-40":LPRINT"P";A$(E,0):LPRINT"M0,0"
3225 IFU1=0THENZZ=0
3235 XX=VAL(A$(E,F))*(410/VV):IFU1=0THENGOSUB3250ELSEGOSUB3260
3240 LPRINT"M-15,"+STR$(YY-6):LPRINT"P";CHR$(E+65);RIGHT$(STR$(F),2)
3245 LPRINT"M0,"+STR$(YY-30):YY=YY-40:NEXTF:NEXTE:LPRINT"M-20,"+STR$(-40*(P+1))
3247 LPRINT"I":LPRINT"C0":LPRINT"M20,0":LPRINT"D450,0":LPRINT"M0,-80":GOTO3000
3250 GOSUB3890:LPRINT"M0,"+STR$(YY):LPRINT"D"+STR$(XX)+","+STR$(YY)
3255 LPRINT"D"+STR$(XX)+","+STR$(YY-30)+",0,"+STR$(YY-30):RETURN
3260 IFF=1THENGOSUB3890:GOTO3270
3265 LPRINT"M"+STR$(X1)+","+STR$(Y1-15):LPRINT"D"+STR$(XX)+","+STR$(YY-15)
3270 LPRINT"M"+STR$(XX-2)+","+STR$(YY-12):LPRINT"Po":X1=XX:Y1=YY:RETURN
3400 IFP>24THEN3000
3405 XY=1:GOSUB3050:LPRINT"M470,24":LPRINT"P* ";C$;" *":FORE=G2TOG3
3420 LPRINT"M450,0":ZZ=0:LPRINT"C0":LPRINT"P";A$(E,0):LPRINT"M235,-235"
3425 LPRINT"I":MM=0:X1=0:FORF=1TOP:MM=MM+VAL(A$(E,F)):NEXTF
3429 FORF=1TOP:GOSUB3890:X2=(6.283*VAL(A$(E,F)))/MM:NN=SIN(X2/2+X1)*50
3430 PP=COS(X2/2+X1)*50:LPRINT"M"+STR$(NN)+","+STR$(PP)
3431 LPRINT"D"+STR$(NN+SIN(X1)*170)+","+STR$(PP+COS(X1)*170)
3432 GOSUB6000:FORYY=X1TOX2+X1-EESTEPEE
3433 LPRINT"M"+STR$(NN+SIN(YY)*170)+","+STR$(PP+COS(YY)*170)
3434 LPRINT"D"+STR$(NN+SIN(YY+EE)*170)+","+STR$(PP+COS(YY+EE)*170):NEXTYY
3435 LPRINT"D"+STR$(NN)+","+STR$(PP)
3436 LPRINT"M";+STR$(NN+SIN(X2/2+X1)*125-5)+","+STR$(PP+COS(X2/2+X1)*125+9)
3437 LPRINT"P";CHR$(E+65);RIGHT$(STR$(F),2):X1=X2+X1:NEXTF
3438 LPRINT"M-235,-320":LPRINT"I":NEXTE:GOTO3000
3800 IFHH=0THENRETURN
3805 LPRINT"L3":LPRINT"M0,0":LPRINT"D0,-480":LPRINT"M210,-480":LPRINT"D210,0"
3810 LPRINT"M220,-430":LPRINT"D220,-910":LPRINT"M430,-910":LPRINT"D430,-430"
3815 LPRINT"L0":RETURN
3820 LPRINT"C0":LPRINT"M0,0":LPRINT"D0,"+STR$(-40*(P+1))
3825 IFHH=0THEN3830
3826 LPRINT"L3":FORXX=34.166TO410STEP34.166:LPRINT"M"+STR$(XX)+",0"
3827 LPRINT"D"+STR$(XX)+","+STR$(-40*(P+1)):NEXTXX:LPRINT"L0"
3830 LPRINT"M430,"+STR$(-40*(P+1)):LPRINT"D430,0":RETURN
3890 ZZ=ZZ+1:IFZZ=4THENZZ=1
3895 LPRINT"C"+STR$(ZZ):RETURN
3950 XY=1:GOSUB3050
3952 LPRINT"M470,0":LPRINT"P* "+C$+" *":EE=470:FORE=G2TOG3
3955 FORF=1TOP:EE=EE-10:IFEE<0THENEE=460:LPRINT"M0,-310":LPRINT"I"
3960 IFF=1THENLPRINT"M"+STR$(EE)+",10":LPRINT"C3":LPRINT"P";A$(E,0):EE=EE-10
3965 LPRINT"M"+STR$(EE)+",0":LPRINT"C1":LPRINT"P";CHR$(E+65);RIGHT$(STR$(F),2)
3970 LPRINT"M"+STR$(EE)+",-18":LPRINT"P : ";B$(E,F)
3975 LPRINT"M"+STR$(EE)+",-134":LPRINT"P :";VAL(A$(E,F)):NEXTF:NEXTE
3976 LPRINT"M0,-340":GOTO3000
5000 IFR=0THENG2=0:G3=0:RETURN
5010 CLS:D$="":E$="":BEEP99,1:INPUT"GROUPE DE DEPART ";D$
5015 BEEP99,1:CLS:INPUT"GROUPE D`ARRIVEE ";E$:IFLEN(D$)<>1ORLEN(E$)<>1THEN5010
5020 G2=ASC(D$)-65:G3=ASC(E$)-65:IFG2>=0ANDG2=<RANDG3>=G2ANDG3<=RTHENRETURN
5030 GOTO5010
6000 EE=VAL(A$(E,F)):IFEE=0THEN6020ELSEIFEE<MM/60THENEE=.017:RETURN
6005 IFEE<MM/30THENEE=.034ELSEEE=.051
6010 RETURN
6020 EE=.0001:RETURN
8000 PRINT"REPERE:(A)VEC/(S)ANS"
8001 A$=INKEY$:IFA$="A"THENHH=1ELSEIFA$<>"S"THEN8001
8002 CLS:RETURN
