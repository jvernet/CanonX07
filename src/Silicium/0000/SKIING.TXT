
110 CONSOLE0,4,0,1,0:CLS:CLEAR:DEFINTA-Z
120 FORI=224TO231:READA$:FONT$(I)=A$:NEXT
130 DATA"4,48,8,16,32,64,128,128","128,128,128,128,128,128,128,128"
140 DATA"128,128,64,32,16,8,4,4","64,96,120,96,64,64,64,64"
150 DATA"48,48,16,24,52,88,144,32","48,48,104,164,32,180,72,72"
160 DATA"48,48,32,96,176,104,36,16","0,0,0,124,8,8,200,248"
170 LOCATE0,1:PRINT"¨¨¨ S K I I N G ¨¨¨"
190 IFINKEY$=""THEN190
200 D=RND(VAL(RIGHT$(TIME$,2)))
210 CLS:PRINT"Touches pour jouer    :(GAUCHE)";
220 G$=INKEY$:IFG$=""THEN220ELSEPRINTG$
230 PRINT"  :(DROITE)";
240 D$=INKEY$:IFD$=""THEN240ELSEPRINTD$
250 CLS:PRINT"Nombre de joueurs?";
260 A$=INKEY$:IFA$=""THEN260ELSENJ=VAL(A$):IFNJ<1ORNJ>4THEN260ELSEPRINTA$;
270 DIMVI(NJ),SC(NJ):FORI=1TONJ:VI(I)=5:NEXT:PRINT"  Difficulte(1-8)?";
280 A$=INKEY$:IFA$=""THEN280ELSED=VAL(A$):IFD<1ORD>8THEN280ELSEPRINTA$:D=9-D
290 FORNP=1TO25:FORJS=1TONJ:IFVI(JS)=0THEN470
295 CLS:PRINT"PISTE No";NP:PRINT"Joueur No";JS:PRINT"Score =";SC(JS)
298 IFINKEY$=""THEN298
300 P=RND(0)*(18-D)+1:X=P+D/2:CLS
310 FORI=1TO50
320 SK$=CHR$(229)
330 IFTKEY(G$)THENX=X-1:SK$=CHR$(228)
340 IFTKEY(D$)THENX=X+1:SK$=CHR$(230)
350 FA$=CHR$(225+IP):FA$=FA$+FA$:IFIP=1THENFA$=CHR$(29)+FA$
360 PRINT:T=SCREEN(X,0):LOCATEX,0:PRINTSK$;
370 LOCATEP-1,3:PRINTFA$;TAB(P+D)FA$;
380 IFPE=0THENPE=RND(0)*16-8
390 IP=SGN(PE):PE=PE-IP:P=P+IP:SC(JS)=SC(JS)+1
400 IFP<1THENP=1:PE=-PE:IP=0ELSEIFP>17-DTHENP=17-D:PE=-PE:IP=0
410 IFT=32THENNEXTI
415 IFI=51THEN440
420 BEEP12,2:VI(JS)=VI(JS)-1:SC(JS)=SC(JS)-10:IFSC(JS)<0THENSC(JS)=0
422 IFVI(JS)=0THENCLS:PRINT"5 chutes !c'est bon pour l'hopital!":GOTO465
425 LOCATEX,0:PRINTCHR$(231);:FORX=D/2TO20:IFSCREEN(X-D/2,0)=32THENNEXT
430 IFINKEY$=""THEN430ELSELOCATE18,3:NEXTI
440 A$=STRING$(2,225):FORI=0TO3:PRINT:LOCATEX,0:PRINTCHR$(229);
450 LOCATEP-IP-1,3:PRINTA$;TAB(P-IP+D)A$;:FORJ=1TO80:NEXTJ,I
455 PRINTTAB(P-IP-1)STRING$(D+3,227);
460 FORI=0TO2:LOCATEX,I:PRINT" ":LOCATEX,I+1:PRINTCHR$(229);:FORJ=1TO99:NEXTJ,I
465 IFINKEY$=""THEN465
470 NEXTJS:S=0:FORJ=1TONJ:S=S+VI(J):NEXT:IFS>0THENNEXTNP
480 MA=0:FORI=1TONJ:IFSC(I)>MATHENMA=SC(I):MJ=I
490 NEXT:INIT£1,"SKI-HS":INPUT£1,HS:IFHS>=SC(MJ)THEN510ELSEPRINT£1,SC(MJ)
500 CLS:PRINT"Joueur No";MJ:INPUT"Quel est votre nom?";NM$:PRINT£1,LEFT$(NM$,8)
510 INIT£1,"SKI-HS":CLS:INPUT£1,HS:PRINT"HIGH SCORE=";HS
520 INPUT£1,NM$:PRINT"REALISE PAR ";NM$:PRINT:PRINT"NOUVELLE PARTIE ?";
530 A$=INKEY$:IFA$=""ORA$<>"O"ANDA$<>"N"THEN530ELSEIFA$="O"THENCLEAR:RUN210
540 END
1000 INIT£1,"SKI-HS",20,"D":PRINT£1,0000:PRINT£1,"        ":RUN
