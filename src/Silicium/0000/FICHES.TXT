
1 ' (C) LOGI'STICK 84
2 KEY$(1)="iniINIT£5,"+CHR$(34)+"COM:"+CHR$(34)
3 KEY$(2)="exeEXEC&HEE1F"
10 CLEAR0:CLEAR (FRE(0)-999)/18*15:DIMH$(0,0):ONERRORGOTO6000
30 CLS:PRINT"Crer un fichier  1 Ouvrir un fichier 2 Sauver un fichier 3 ";
40 PRINT"Choix";:GOSUB5000
50  IF OK$<>CHR$(13) THEN 130ELSECLS
70 PRINT"Entrer des fiches 4 Lire des fiches   5 Rech/Mod fiches   6 Choix";
81 GOSUB 5000
90 IF OK$<>CHR$(13) THEN 130ELSECLS
110 PRINT"Dtruire fiche(s) 7 Trier des fiches  8 Editer des fiches 9 Choix";
121 GOSUB 5000:IFOK$<>CHR$(13)THEN130ELSECLS
125 PRINT"Cartes            C Arret             O Fin               F RAM";
126 PRINT"               R";:GOSUB5000
130 IF OK$="O" THEN SLEEP:GOTO 30
135 IFOK$="C"THEN1500
140 IF OK$="F" THEN CLS: END
145 IFOK$="R"THENCLS:PRINT"RAM =";FRE(K$);:GOSUB5000
150 X=VAL(OK$):ON X GOTO 160,230,270,330,420,500,660,740,930:GOTO 30
160 CLS:PRINT"Nom fichier : ";:N=0:F$="":FOR I=1 TO 8
161 GOSUB 5000
162 IFOK$=CHR$(13) THEN 170
163 PRINT OK$;:F$=F$+OK$:NEXT I
170 CLS:FOR I=1 TO 10
180 PRINT"Rub ";I;" : ";:R$(I)="":FOR J=1 TO 8
181 GOSUB 5000
182 IFOK$="^" AND J=1 AND I=1 THEN 30ELSEIFOK$="^"ANDJ=1THENI=I-2:GOTO 187
183 IFOK$=CHR$(13)AND J=1 THEN 210 ELSE IFOK$=CHR$(13)THEN187
185 IFOK$=CHR$(29)THENJ=J-1:PRINTOK$;:R$(I)=LEFT$(R$(I),LEN(R$(I))-1):GOTO181
186 PRINT OK$;:R$(I)=R$(I)+OK$:NEXT J
187 PRINT:NEXT I
210 T=I-1
215 NB=INT(FRE("£")/((T+1)*15))
216 ERASEH$:DIM H$(NB,T)
217 CLS:PRINT:PRINT"Nb fiches : ";NB:GOSUB 5000
219 GOTO 30
230 CLS:PRINT"Nom fichier : ";:N$="":FOR I=1 TO 8
231 GOSUB 5000
232 IFOK$=CHR$(13) THEN 240
233 PRINTOK$;:N$=N$+OK$:NEXT I
240 INIT£1,"CASI:":INPUT£1,F$,T,N,NB,C
241 IFF$<>N$THEN30ELSECLS:PRINT"CHARGE ";N$
242 ERASE H$:DIM H$(NB,T)
250 INPUT£1,R$(1),R$(2),R$(3),R$(4),R$(5),R$(6),R$(7),R$(8),R$(9),R$(10)
251 IF N=8 THEN 30
260 FOR I=1 TO N:FOR J=1 TO T:INPUT£1,H$(I,J):NEXT J:NEXT I:GOTO 30
270 IF T=0 OR N=0 THEN 30
280 CLS:LINEINPUT"Pret sauvegarde : ";K$:IFK$="^" THEN 30
300 INIT£1,"CASO:":PRINT£1,F$,T,N,NB,C
301 PRINT£1,R$(1),R$(2),R$(3),R$(4),R$(5),R$(6),R$(7),R$(8),R$(9),R$(10)
310 IF N=0 THEN 30
320 FOR I=1 TO N:FOR J=1 TO T:PRINT£1,H$(I,J):NEXT J :NEXT I:GOTO 30
330 IF T=0 THEN 30
340 N=N+1:IF N=NB+1 THEN N=NB :GOTO 30
350 CLS:PRINT"Fiche numro ";N:FOR Z=1 TO 50:NEXT Z:CLS
360 FOR I=1 TO T
370 PRINT R$(I);" : ";:H$(N,I)="":FOR K=1 TO 20
371 GOSUB 5000
372 IFOK$=CHR$(13) THEN 380
374 IF K=16 THEN BEEP 10,10
375 PRINT OK$;:H$(N,I)=H$(N,I)+OK$:NEXT K
380 PRINT:IF H$(N,1)="^" THEN N=N-1:GOTO 30
390 IF H$(N,I)="^" THEN I=I-1:GOTO 370
400 IF H$(N,I)="/"THENH$(N,I)=H$(N-1,I):PRINTCHR$(30);R$(I);" : ";H$(N,I)
410 NEXT I:GOTO 340
420 IF T=0 OR N=0 THEN 30
421 CLS :PRINT"1/IMP 2/LEC : ";
422 GOSUB 5000
423 IF OK$="^" THEN 30
424 MI=VAL(OK$):IF MI<1OR MI>2 THEN 422
430 CLS:FOR I=1 TO N
449 IF MI=1 THEN LPRINT:LPRINT"NUMERO : ";I
450 PRINT:PRINT"NUMERO : ";I:FOR J=1 TO T:PRINT R$(J);" : ";H$(I,J)
451 IF MI=1THEN LPRINTR$(J);" : ";H$(I,J)
455 IF INKEY$=CHR$(32)THEN GOSUB 5000
456 IF INKEY$="^"THEN30
457 NEXT J:NEXT I:GOTO 30
500 IF N=0 OR T=0 THEN 30
510 CLS:INPUT"Rech. rub. ";C$:C=VAL(C$):IFC$="^"THEN 30ELSEIFC<1ORC>T THEN510
530 CLS:PRINT R$(C);" : ";:FOR I=1 TO 20
535 GOSUB 5000
536 IFOK$=CHR$(13)THEN 560
538 IF OK$="^" AND I=1  THEN 30
539 PRINTOK$;:R$=R$+OK$:NEXT I
560 FOR I=1 TO N
570 IF LEFT$(H$(I,C),LEN(R$))=R$ THEN580 ELSE NEXTI:R$="":GOTO 30
580 CLS: FOR K=1 TO T : PRINT R$(K);" : ";H$(I,K):NEXT K
590 GOSUB 5000
600 IFOK$="M" THEN 610 ELSE NEXT I:GOTO 30
610 CLS:FOR J=1 TO T:PRINTR$(J);" : ";H$(I,J);
620 PRINTTAB(LEN(R$(J))+3 );:LINE INPUT H$(I,J)
621 IF LEN(H$(I,J))>20 THEN 620
630 NEXT J
640 GOSUB 5000
650 IF OK$="N" THEN 610 ELSE  NEXT I:GOTO 30
660 IF T=0 OR N=0 THEN 30
670 CLS:INPUT"Fiche ‰ dt. ";C$:C=VAL(C$):IFC$="^"THEN30ELSEIFC<1ORC>N THEN 670
690 CLS:FOR I=1 TO T :PRINT R$(I);" : ";H$(C,I):NEXT I
720 CLS:PRINT"Destruction D.A.R";:GOSUB5000:IFOK$="A"ANDC<NTHENC=C+1:GOTO690
721 IF OK$="R" AND C>1 THEN C=C-1:GOTO 690
722 IFOK$=CHR$(84)THEN30
723 IFOK$="D"THEN730ELSE720
730 FORI=CTON-1:FOR J=1TOT:H$(I,J)=H$(I+1,J):NEXTJ:NEXTI:N=N-1:GOTO30
740 IFT=0ORN=0THEN30
750 CLS:INPUT"Trie sur rub ";C$:C=VAL(C$):IFC$="^"THEN30ELSEIFC<1ORC>TTHEN750
770 CLS:PRINT"Alphanumrique    1":PRINT"Numrique         2"
780 PRINT"Choix"
781 GOSUB5000
782 K=VAL(OK$):IFOK$="^"THEN30ELSEIFK<1ORK>2THEN781
790 D=1
800 D=2*D
810 IFD<=NTHEN800
820 D=INT((D-1)/2)
830 IFD=0THEN30
840 FORI=1TON-D
850 FORJ=ITO1STEP-D
860 L=J+D
865 IFK=2THENIFVAL(H$(J,C))<VAL(H$(L,C))THEN910ELSE890
870 IFH$(J,C)<=H$(L,C)THEN910
890 FORZ=1TOT:X$=H$(J,Z):H$(J,Z)=H$(L,Z):H$(L,Z)=X$:NEXTZ
900 NEXTJ
910 NEXTI
920 GOTO820
930 IFT=0ORN=0THEN30
940 CLS:INPUT"Nombre rub ";C$:C=VAL(C$):IFC$="^"THEN30ELSEIFC<1ORC>TTHEN940
960 CLS:L=0:FORJ=1TOC
961 INPUT"Rub";C(J):IFC(J)<1ORC(J)>TTHENPRINTCHR$(30);:GOTO961
965 INPUT"Lar ";L(J):L=L+L(J):NEXTJ
970 IFL>80THEN960
980 CLS:INPUT"Rech. rub ";D:IFD<1ORD>TTHEN980
990 X$="":LINEINPUT"Choix (ret) : ";X$:IF X$=""THENX$="ENTIER"
1000 CLS:LINEINPUT"Titre : ";TI$:LINEINPUT"Comment.";CO$:LINEINPUT"Inter.";I$
1130 IF L>40 THEN XX=1
1140 IF L<=40 THEN XX=2
1170 IF TI$="" THEN LPRINT °XX,2§"EDITION DU FICHIER ";F$;"/";X$:GOTO 1190
1180 LPRINT°XX,2§TI$
1190 FOR I=1 TO C
1200 LPRINT°XX,3§LEFT$(R$(C(I)),L(I)-1);" ";
1210 K=L(I)-LEN(R$(C(I)))-1
1220 IF K>0 THEN LPRINT °XX,3§STRING$(L(I)-LEN(R$(C(I)))-1,32);
1230 NEXT I
1240 LPRINT:LPRINT
1250 FOR I=1 TO N
1251 IF I$<>""THEN LPRINT
1260 IFX$="ENTIER"THEN1280
1270 IF LEFT$(H$(I,D),LEN(X$))<>X$ THEN 1340
1280 FOR J=1 TO C
1290 LPRINT°XX,1§LEFT$(H$(I,C(J)),L(J)-1);" ";
1300 K=L(J)-LEN(H$(I,C(J)))-1
1310 IF K>0 THEN LPRINT°XX,1§STRING$(L(J)-LEN(H$(I,C(J)))-1,32);
1320 NEXT J
1330 LPRINT
1340 NEXT I
1350 LPRINT:LPRINT°XX,2§CO$
1360 GOTO 30
1500 CLS:PRINT"Sauvegarde        1 Lecture           2 Effacement        3";
1510 PRINT" Directory         4";:GOSUB5000:C$="":IFOK$=CHR$(13)THEN30
1520 ON VAL(OK$)GOTO 1600,1700,1800,1900:GOTO30
1600 IFN=0THEN30ELSECLS:PRINTF$;" SAUVEGARDE":GOSUB5000:IFOK$=CHR$(94)THEN30
1610 INIT£1,F$,200+N*T*25:PRINT£1,F$,T,N,NB,C
1620 PRINT£1,R$(1),R$(2),R$(3),R$(4),R$(5),R$(6),R$(7),R$(8),R$(9),R$(10)
1630 FORI=1TON:FORJ=1TOT:PRINT£1,H$(I,J):NEXTJ:NEXTI:GOTO1900
1700 CLS:PRINT"Fichier:";:FORI=1TO6:GOSUB5000:IFOK$=CHR$(13)THEN1710
1705 C$=C$+OK$:PRINTOK$;:NEXTI
1710 INIT£1,C$:INPUT£1,F$,T,N,NB,C:ERASEH$:DIMH$(NB,T)
1720 INPUT£1,R$(1),R$(2),R$(3),R$(4),R$(5),R$(6),R$(7),R$(8),R$(9),R$(10)
1730 FORI=1TON:FORJ=1TOT:INPUT£1,H$(I,J):NEXTJ:NEXTI:GOTO420
1800 CLS:PRINT"Fichier:";:FORI=1TO6:GOSUB5000:C$=C$+OK$:PRINTOK$;:NEXTI
1810 DELETEC$,"D":GOTO1500
1900 CLS:DIR
1910 IFINKEY$=""THEN1910ELSE1500
5000 OK$=INKEY$:IFOK$=""THEN5000ELSEIFASC(OK$)<32ANDASC(OK$)>27THEN5000
5010 RETURN
6000 RESUME30
