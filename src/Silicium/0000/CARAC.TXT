
5  '**********************************
10 '***** Nom du programme:KARAK *****
20 '***** AUTEUR: MAREK Guy      *****
30 '***** Computer: CANON X-07   *****
40 '***** Sortie Ecran et Imprim *****
50 '***** But:CREATION DE SPRITE *****
60 '***** Saisie sur grille avec *****
70 '***** reproduction en talle 1*****
80 '***** Menu par option-Index  *****
90 '**********************************
100 CLS:CLEAR 150:AF$="Generation Sprites":GOSUB 1000
110 FOR I=7TO 35 STEP3:LINE (0,I)-(18,I):NEXT
120 FORI=0TO20STEP3:LINE(I,7)-(I,31):NEXT
130 X=64:Y=9:Z=31:FORW=22TO45STEP2:LINE(W,Y)-(X,Y):LINE-(X,Z):LINE-(W,Z)
140 LINE-(W,Y):X=X-2:Y=Y+2:Z=Z-2:NEXTW:X=64:Y=9:Z=31:FORW=22TO25STEP3
150 LINE(W,(Z-Y)/2+Y)-((X-W)/2+W,Y):LINE-(X,(Z-Y)/2+Y):LINE-((X-W)/2+W,Z)
160 LINE-(W,(Z-Y)/2+Y):X=X-3:Y=Y+3:Z=Z-3:NEXTW:LINE(47,14)-(39,14):LINE-(39,25)
170 LOCATE11,1:PRINT"* code *"
175 LOCATE 12,2:PRINT"°";CHR$(139);"‰‰‰§";:X=1:Y=8:W=0:Z=0
180 IF POINT(X+1,Y+1)=-1 THEN PSET (X,Y)
185 X=X+W:Y=Y+Z:W=0:Z=0:T2=30
190 PSET (X,Y):GOSUB1100:PRESET(X,Y):GOSUB 1100
200 T$=INKEY$:IFT$=""THEN 190 ELSEIFT$=CHR$(13)THEN300 ELSE IF T$="F"THEN 400
202 IFT$="B"ORT$="N"THENGOSUB500:GOTO190ELSEIFT$="K"THEN1500
204 IF T$="I" THEN1790ELSEIFT$="E"THENGOSUB1700ELSEIFT$="R"THEN2200
205 IFT$="P"THEN2300ELSEIFT$="V" THEN GOSUB 1300:GOTO 190
210 IF T$=CHR$(29)AND X>1 THEN W=-3:GOTO180
220 IF T$=CHR$(28)AND X<15 THEN W=3:GOTO180
230 IF T$=CHR$(30)AND Y>8THEN Z=-3:GOTO 180
240 IF T$=CHR$(31)AND Y<29 THEN Z=3:GOTO180
250 IF T$="9" THEN GOSUB260ELSEIFT$="0"THENGOSUB270ELSEIFT$="C"THEN1580ELSE190
255 GOTO 280
260 PSET (X,Y):PSET(X+1,Y):PSET(X,Y+1):PSET(X+1,Y+1):RETURN
270 PRESET(X,Y):PRESET(X+1,Y):PRESET(X,Y+1):PRESET(X+1,Y+1):RETURN
280 IF X=16 AND Y=29THEN 190 ELSE IF X=16 THEN X=1:Y=Y+3 ELSE X=X+3
290 GOTO 190
300 GOSUB 1300
310 V$=RIGHT$(V$,LEN(V$)-1)
320 LOCATE 13,2:PRINTCHR$(139);:BEEP9,10:R$="":GOSUB 1400
330 IF R$=""THEN 320ELSE R=VAL(R$)
340 IF R=0 THEN 320 ELSE IF R>127ANDR<160THEN 360
350 IF R>223ANDR<256THEN360ELSEAF$="128-159":LOCATE12,3:GOSUB1000
355 T2=300:GOSUB1100:LOCATE12,3:AF$="224-255":GOSUB1000:GOTO320
360 LOCATE12,3:PRINT"       ";
370 T$=INKEY$:IF T$=""THENGOSUB 1200:GOTO370 ELSE IF T$="C" THEN 175
380 IF T$<>"V"THEN370 ELSET$="B":FONT$(R)=V$:GOSUB500:GOTO175
400 CLS:AF$="Good Bye Thank You!!":PRINT:GOSUB 1000:T2=250:GOSUB 1100:CLS:END
500 X1=X:Y1=Y
510 FORX=1TO17STEP3:FORY=8TO30STEP3
520 IFT$="B"THENGOSUB270ELSEGOSUB260
530 NEXTY,X:X=X1:Y=Y1:RETURN
1000 FOR I=1 TO LEN(AF$):L$=MID$(AF$,I,1)
1010 FOR T=0 TO 10:NEXT:PRINTL$;:BEEP 35,1:NEXT I:RETURN
1100 FOR T=0TOT2:NEXT:RETURN
1200 T2=10:FOR I=73 TO 105STEP2:PSET (I,25):PSET(I,28):GOSUB 1100:BEEP 5,1
1210 PRESET(I-2,25):PRESET(I-2,28):GOSUB1100:BEEP 10,1:NEXT:RETURN
1300 FOR I=15TO24:FORJ=40TO47:PRESET(J,I):NEXT J,I
1305 V$="":FOR I=8 TO29 STEP 3:B=128:V=0
1310 FOR J=1 TO 16 STEP 3
1320 IF POINT(J,I+1)=-1 THENV=V+B:PSET(40+(J+2)/3,(I-5)/3+15):BEEP1,1
1330 B=INT(B/2):NEXT J
1340 W$=STR$(V):V$=V$+","+RIGHT$(W$,LEN(W$)-1):NEXT I
1350 RETURN
1400 FORXT=1TO3
1410 LOCATE13+XT,2:PRINT"ˆ";:T2=50:GOSUB1100:LOCATE13+XT,2:PRINT" ";:GOSUB1100
1420 T$=INKEY$:IFT$=""THEN1410ELSELOCATE13+XT,2:PRINTT$;:VK=ASC(T$)
1430 IF VK<48ORVK>57THEN1410ELSER$=R$+T$:NEXTXT:RETURN
1500 T2=80:BEEP30,1:GOSUB1100:T$=INKEY$:IFT$=""THEN1500ELSEK$=FONT$(ASC(T$))
1510 X1=X:Y1=Y:Y=8:DL=8:DB=1:IFMID$(K$,23,2)="00"THENY=11:DL=7
1515 T$="B":GOSUB500
1520 FORD=DBTODL:V$=MID$(K$,D*3-1,2):GOSUB1550:VL=VL/4:FORX=16TO1STEP-3
1530 BI=VL/2-INT(VL/2):VL=INT(VL/2):IFBI=0THENGOSUB270ELSEGOSUB260
1540 NEXTX:Y=Y+3:NEXTD:X=X1:Y=Y1:GOTO190
1550 VL=0:FORD2=1TO2:V=ASC(MID$(V$,D2,1)):IFV>57THENV=V-55ELSEV=V-48
1560 IFD2=1THENV=V*16
1570 VL=VL+V:NEXTD2:RETURN
1580 R$="":GOSUB 1400:R=VAL(R$)
1590 LOCATE14,2:PRINT"‰‰‰";:IFR>255THEN1580ELSEK$=FONT$(R):GOTO1510
1600 R$="":GOSUB 1400:K$=FONT$(VAL(R$)):LOCATE14,2:PRINT"‰‰‰";:GOTO1510:
1700 AF$="Guide-Explicatif":CLS
1710 FORI=0TO19:FORJ=18TOISTEP-1:A$=MID$(AF$,I+1,1):LOCATEJ,0:PRINTA$;
1720 LOCATEJ+1,0:PRINT" ";:BEEPI*J,1:NEXTJ,I:LOCATE19,0:PRINTMID$(AF$,20,1);
1730 LOCATE0,1:PRINTSTRING$(20,"-");:CONSOLE2,2
1740 LOCATE0,2:PRINT"Dessinez sur la Grille de Saisie votre ";:GOSUB2100
1745 IFT=1THENBEEP45,5:GOSUB2100:GOTO1745
1750 LOCATE0,2:PRINT"caractere.Reproduction avec Echelle no1";:GOSUB2100
1760 IFT=1THEN1740
1770 LOCATE0,2:PRINT"Indiquez ensuite le Code Correspondant ";:GOSUB2100
1775 IFT=1THEN1750
1780 LOCATE0,2:PRINT"Et (V)alidez ou (C)orrigez votre entre";:GOSUB2100
1785 IFT=1THEN1770ELSECONSOLE0,4:GOTO100
1790 CONSOLE0,4:CLS:AF$="Option-Deplacement":GOSUB1000:CONSOLE1,3
1800 LOCATE0,1:PRINTCHR$(141);"...Direction Haut  ";CHR$(142);
1810 PRINT"...Direction Bas   ";CHR$(139);"...Directi. Droite";:GOSUB2100
1820 IFT=1THENBEEP55,5:GOSUB2100:GOTO1820
1830 LOCATE0,1:PRINTCHR$(140);"...Direction Gauche0...Effacement Carr";
1840 PRINT"9...Dessin du Carr";:GOSUB2100
1850 IFT=1THEN1800
1860 CONSOLE0,4:CLS:AF$=" Options-Menu ":GOSUB1000:CONSOLE1,3
1870 LOCATE0,1:PRINT"Return ..Fin grille (F)in du programme  (V)isualisation no1";
1875 GOSUB 2100
1880 IFT=1THENBEEP65,1:GOSUB2100:GOTO1880
1890 LOCATE0,1:PRINT"(I)ndex des options (K)ey:Dessin touche (C)hiffre code cara";
1900 GOSUB 2100:IFT=1THEN1870
1910 LOCATE0,1:PRINT"(N)oircissemnt Ecran(B)lanchissemt Ecran(E)xplicatif  ";
1920 GOSUB2100:IFT=1THEN1890
1930 LOCATE0,1:PRINT"(R)eproduction ecran(D)essin du sprite  ------------------";
1940 GOSUB2100:IFT=1THEN1910ELSECONSOLE0,4:GOTO100
2100 T$=INKEY$:IFT$=CHR$(31)THENT=0ELSEIFT$=CHR$(30)THENT=1ELSE2100
2110 RETURN
2200 ' reproduction grille et dessin
2210 LPRINTCHR$(17);CHR$(13);CHR$(18):LPRINT"L2,D488,0"CHR$(13)"M0,0"
2215 LPRINT"A"
2220 LPRINT°3,0§"*** ";:LPRINT°3,3§"REPRODUCTION ECRAN ";:LPRINT°3,0§"***";
2230 LPRINT°2,0§:LPRINTCHR$(18):LPRINT"M0,50"CHR$(13)"I":LPRINT"S2,L2,D488,0"
2240 LPRINTCHR$(13):LPRINT"I":LPRINT"M-488,-10":LPRINT"I":LOCATE12,3
2242 AF$="Couleur":GOSUB1000
2245 T$=INKEY$:IFT$=""THEN2245ELSEIFASC(T$)<48ORASC(T$)>51THEN2245
2255 LPRINT"C"+T$:LPRINT"M4,0":LPRINT"I"
2260 FORJ=7TO31:FORI=0TO64:IFPOINT(I,J)=-1THENGOSUB2400
2270 LPRINT"M4,0"CHR$(13)"I":NEXTI:LPRINT"M-260,-5"CHR$(13)"I":NEXTJ
2280 LPRINT "M335,85":LPRINT"I":FORI=0TO3:LPRINT"Q"+STR$(I)+",PCODE":NEXT
2290 LPRINT"Q0,C3,M12,-40":IFR$=""THENLPRINT"P***";ELSELPRINT"P"+R$
2295 LPRINT"           ":LPRINTCHR$(17):LPRINTCHR$(17):LPRINTCHR$(17):GOTO190
2300 'Dessin du Caractere
2310 LPRINTCHR$(17):LPRINT°3,0§"Caractere No:";
2320 IFR$=""THENLPRINT"***";ELSELPRINTR$;
2330 LPRINT" ";CHR$(127);" ";:LPRINTCHR$(18)
2335 LPRINTCHR$(18):LPRINT"0,0"CHR$(13)"I"CHR$(13)"M335,85"CHR$(13)"I"
2340 FORI=15TO24:FORJ=40TO47:IFPOINT(J,I)=-1THENGOSUB2400
2350 LPRINT"M4,0"CHR$(13)"I":NEXTJ:LPRINT"M-32,-5"CHR$(13)"I":NEXTI
2360 LPRINTCHR$(17);:LPRINT"   ";CHR$(127):LPRINTCHR$(17)
2370 GOTO 190
2400 LPRINT"J4,0,0,-4,-4,0,0,4,4,-1,-4,-1,4,-1,-4,-1,4,0"
2410 RETURN
