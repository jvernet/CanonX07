
0 REM ** NAVIGATION **
2 REM (C) LOGI'STICK 85 & Yvon Puill
4 CONSOLE0,4:REM VERSION 2,3 PLAISANCE
6 CLS:FORI=1TO15STEP2:CIRCLE(15,15),I:CIRCLE(104,15),I:BEEPI,1:NEXT
8 FORI=0TO3:LOCATE3,I:PRINT"              ";:NEXT
10 PRESET(16,15):PRESET(17,13):PRESET(17,17):PRESET(103,15)
12 PRESET(102,13):PRESET(102,17)
14 FORI=0TO31STEP2:LINE(15,I)-(104,I):BEEP31-I,1:NEXT
16 PRESET(29,8):PRESET(29,10):PRESET(29,12):PRESET(29,14)
18 LOCATE5,1:PRINT"NAVIGATION";
20 FORI=0TO48:BEEPI,1:NEXT
22 IFINKEY$=""THEN22
24 FONT$(144)="144,168,168,112,32,32,32,0":FONT$(145)="32,80,32,0,0,0,0,0"
26 FSET2600
28 CLS:PRINT"1 CALCULS DE ROUTES","2 CALCULS ASTRO","3 ACCES FICHIERS"
30 PRINT"4 FIN      ";:INPUT"choix";A:CLS:ONAGOTO376,38,276,34
32 GOTO28
34 CONSOLEà0,1,0:CLS:END
36 '
38 INIT£2,"ETOILE":CLEAR500:INPUT£2,M
40 DIMST$(M),HD(M),HM(M),DD(M),DM(M),DP$(M)
42 FORI=1TOM:INPUT£2,ST$(I),HD(I),HM(I),DD(I),DM(I),DP$(I):NEXT
44 '
46 PI=4*ATN(1):RD=180/PI
48 CLS:INPUT"Date:j,m,anne      (--,--,----)";J,MO,AN:CLS
50 INPUT"Heure TU du point:  h,mn (--,--)";H,MI:CLS
52 INPUT"e:,',pàle         (--,--.-,-)";D,MN,P$:CLS:GOSUB206:FI=D
54 INPUT"Ge:,',pàle         (---,--.-,-)";D,MN,P$:CLS:GOSUB206:G=D
56 '
58 CONSOLE0,4:CLS
60 CLS:PRINT"1 Prpa. du point","2 Point d'toile","3 Droite de Hteur"
62 PRINT"4 MENU     ";:INPUT"choix";A:CLS:ONAGOTO66,100,806,28
64 GOTO58
66 GOSUB258
68 '
70 LPRINT°1,1§DATE$,TIME$:LPRINT°3,0§
72 LPRINT"  PREPARATION DU POINT":LPRINT°3,3§"  Date ";A$;J;MO;AN
74 LPRINT" ";H;" ";"h";MI:LPRINT°2,1§
76 HO=H+(MI/60):GOSUB218
78 FORK=1TOM:D=HD(K):MN=HM(K):GOSUB206:AV=D
80 D=DD(K):MN=DM(K):P$=DP$(K):GOSUB206:DC=D
82 P1=AH+AV+G:IFP1>PITHENP=(2*PI)-P1ELSEP=P1
84 GOSUB236
86 IFHE>10THENLPRINTST$(K),USING"Azimut £££.£";AZ
88 IFHE>10THENLPRINTUSING"              Hauteur ££.£";HE
90 IFHE>10THENPRINTST$(K),USING"Azimut £££.£";AZ:PRINTUSING"Hauteur ££.£";HE
92 IFHE>10THENLPRINT
94 NEXTK
96 GOTO58
98 '
100 CLS:INPUT"Route vraie ";RV:RV=90-RV
102 CLS:INPUT"Vitesse en Nds (--.-):";V:HP=H+(MI/60)
104 CLS:INPUT"Elvation de l'oeil en m (--.-):";MT
106 N=0
108 N=N+1
110 CONSOLE0,4:CLS:PRINT"Etoile No";N:INPUT"Nom (en ttes lettres)";S$:S$(N)=S$
112 CLS:PRINTS$(N):CONSOLE1,3:K=0
114 K=K+1
116 IFST$(K)=S$THEN120ELSE118
118 IFK=MTHEN110ELSE114
120 D=HD(K):MN=HM(K):GOSUB206:AV=D
122 D=DD(K):MN=DM(K):P$=DP$(K):GOSUB206:DC=D
124 CLS:INPUT"Heure TU de l'observation(h,mn,s)";HO,MM,S:HO=HO+(MM/60)+(S/3600)
126 CLS:GOSUB218
128 P1=AH+AV+G:IFP1>PITHENP=(2*PI)-P1ELSEP=P1
130 CLS:INPUT"Hauteur observe:,' (--,--.-)";D,MN:GOSUB206:HV=D*RD
132 HV=(HV-((1.78*SQR(MT))+TAN((90-HV)/RD))/60)
134 CLS:GOSUB234:AZ(N)=AZ:IT(N)=(HV-HE)*60:CONSOLE0,4
136 DI(N)=V*(HP-HO)
138 PRINTUSING"Azimut £££.£";AZ(N):PRINTUSING"Inters. ££.£";IT(N)
140 INPUT"Une autre toile";X$:IFX$="O"THEN108
142 CLS:INPUT"Schma sur Imp.(O/N)";X$:IFX$="N"THEN58
144 '
146 LPRINT°1,1§DATE$,TIME$
148 CLS:LPRINT°3,0§:LPRINT"   ** POINT D'ETOILE **":LPRINT
150 GOSUB258:LPRINT°3,3§"   Date ";A$;J;MO;AN,"       ";H;"h";MI;"TU"
152 LPRINT°2,2§"Route vraie:";ABS(RV-90),"Vitesse:";V;"Nds"
154 LPRINTCHR$(18),"M240,-250":LPRINT"I"
156 X1=260*COS(RV/RD):Y1=260*SIN(RV/RD)
158 LPRINT"C3,D"+STR$(X1)+","+STR$(Y1)+","+STR$(-X1)+","+STR$(-Y1)+",0,0"
160 LPRINT"C1,D0,250,0,-250,0,0,240,0,-240,0,0,0"
162 FORI=0TO12
164 LPRINT"M0,"+STR$((20*I)+1):LPRINT"Q1,S0,Pù":LPRINT"S0,Q0,P "+STR$(I):NEXT
166 FORI=1TO20:LPRINT"M"+STR$((ABS(20*COS(FI))*I)-2)+",0":LPRINT"Pù":NEXT
168 LPRINT"M0,0"
170 FORI=1TON:X2(I)=DI(I)*COS(RV/RD)*20:Y2(I)=DI(I)*SIN(RV/RD)*20
172 LPRINT"M"+STR$(X2(I))+","+STR$(Y2(I))
174 Y3(I)=200*COS(AZ(I)/RD):X3(I)=200*SIN(AZ(I)/RD)
176 Y4(I)=240*COS((AZ(I)-90)/RD):X4(I)=240*SIN((AZ(I)-90)/RD)
178 LPRINT"L13,C2,J"+STR$(X3(I))+","+STR$(Y3(I)):LPRINT"S1,P*"+STR$(I)
180 FORJ=1TO2
182 LPRINT"M"+STR$(X2(I))+","+STR$(Y2(I))
184 LPRINT"R"+STR$(20*IT(I)*X3(I)/240)+","+STR$(20*IT(I)*Y3(I)/240)
186 LPRINT"C0,L0"
188 LPRINT"J"+STR$(X4(I))+","+STR$(Y4(I))+","+STR$(-X4(I)*2)+","+STR$(-Y4(I)*2)
190 LPRINT"J"+STR$(X4(I))+","+STR$(Y4(I)):NEXTJ
192 LPRINT"H":NEXTI
194 LPRINT"M-240,-250":LPRINT"A"
196 FORI=1TON:LPRINT°2,1§"Etoile No";I;S$(I)
198 LPRINTUSING"Azimut: £££.£";AZ(I):LPRINTUSING"Intercepte: ££.£";IT(I)
200 LPRINT:NEXTI
202 GOTO58
204 '
206 D=(D+(MN/60))/RD
208 IFP$="S"THEND=-D
210 IFP$="W"THEND=-D
212 P$="0"
214 RETURN
216 '
218 INIT£1,"VERNAL"
220 FORI=1TOMO:INPUT£1,A0,A1:NEXTI
222 T=(2*(J+(HO/24))/33)-1
224 DEFFND(X)=-ATN(X/SQR(1-X^2))+PI/2
226 AH=(A0+(A1*COS(FND(T))))/360:AH=(AH-CINT(AH))*360
228 IFAH<0THENAH=AH+360
230 AH=AH/RD
232 RETURN
234 '
236 AA=(TAN(DC)*COS(FI))-(SIN(FI)*COS(P))
238 AZ=RD*ATN(SIN(P)/AA)
240 HE=(SIN(DC)*SIN(FI))+(COS(DC)*COS(FI)*COS(P))
242 HE=(ATN(HE/SQR(1-HE^2)))*RD
244 IFAA<0ANDFI>0THENAZ=180+AZELSEAZ=AZ
246 IFAA<0ANDFI<0THENAZ=180+AZELSEAZ=AZ
248 IFP1<PITHENAZ=360-AZ
250 IFAZ>360THENAZ=AZ-360
252 IFAZ<0THENAZ=360+AZ
254 RETURN
256 '
258 DATASam,Dim,Lun,Mar,Mer,Jeu,Ven
260 FORX=0TO6:READA$(X):NEXTX
262 JO=J:M1=MO
264 IFM1<=2THENJO=JO-1
266 IFM1<=2THENM1=M1+12
268 T=JO+INT((M1+1)*(13/5))+INT(5*AN/4)-INT(AN/100)+INT(AN/400)
270 T=INT((T/7-INT(T/7))*7+.1):A$=A$(T)
272 RETURN
274 '
276 CLS:PRINT"1 fichier ETOILES","2 fichier P. VERNAL","3 fichier SOLEIL"
278 PRINT"4 MENU     ";:INPUT"choix";A:ONAGOTO300,284,358,28
280 GOTO276
282 '
284 CLS:PRINT"fichier P. VERNAL":CONSOLE1,3
286 INIT£1,"VERNAL",300,"D":DIMA0(12),A1(12)
288 FORI=1TO12:CLS:PRINT"mois No";I:CONSOLE2,2
290 PRINT"A0(";I;")";:INPUTA0(I):PRINT"A1(";I;")";:INPUTA1(I):CONSOLE1,3:NEXTI
292 CONSOLE0,4:CLS
294 FORI=1TO12:PRINT£1,A0(I),A1(I):NEXTI
296 GOTO276
298 '
300 CLS:PRINT"1 Ouverture fichier","2 ajout d'toile","3 mise ‰ jour"
302 PRINT"4 MENU     ";:INPUT"choix";A:ONAGOTO308,330,340,276
304 GOTO300
306 '
308 INIT£2,"ETOILE",1000,"D":CLEAR400
310 DIMST$(40),HD(40),HM(40),DD(40),DM(40),DP$(40)
312 CLS:PRINT"Entre des toiles":M=0
314 CLS:M=M+1:INPUT"Nom de l'toile";ST$(M):CLS:PRINTST$(M):CONSOLE1,3
316 CLS:INPUT"Ascension verse:,' (---,--.-)";HD(M),HM(M)
318 CLS:INPUT"Dclinaison:,',pàle (--,--.-,-)";DD(M),DM(M),DP$(M)
320 CONSOLE0,4:CLS:INPUT"Une autre toile (O/N)";X$:IFX$="O"THEN314
322 PRINT£2,M
324 FORI=1TOM:PRINT£2,ST$(I),HD(I),HM(I),DD(I),DM(I),DP$(I):NEXTI
326 GOTO300
328 '
330 INIT£2,"ETOILE":CLEAR400:INPUT£2,M
332 DIMST$(40),HD(40),HM(40),DD(40),DM(40),DP$(40)
333 FORI=1TOM:INPUT£2,ST$(I),HD(I),HM(I),DD(I),DM(I),DP$(I):NEXTI
334 CLS:LPRINT°1,2§"Etoiles deja en memoire:"
335 FORI=1TOM:LPRINT°1,1§ST$(I);" / ";:NEXTI
336 GOTO314
338 '
340 INIT£2,"ETOILE":CLEAR400:INPUT£2,M:K=0
342 DIMST$(M),HD(M),HM(M),DD(M),DM(M),DP$(M)
344 FORI=1TOM:INPUT£2,ST$(I),HD(I),HM(I),DD(I),DM(I),DP$(I):NEXTI
346 K=K+1
348 CLS:PRINTST$(K):CONSOLE1,3:PRINT"Minutes d'AV:";HM(K)
350 INPUT"mise ‰ jour:";HM(K)
352 CLS:PRINT"Minutes de dcl.:";DM(K):INPUT"mise ‰ jour:";DM(K)
354 CONSOLE0,4:CLS:INPUT"Une autre toile (O/N)";X$:IFX$="O"THEN346ELSE300
356 '
358 CLS:PRINT"fichier SOLEIL":CONSOLE1,3
360 INIT£3,"SOLEIL",200,"D":PRINT"Entre Coef. AHso":CONSOLE2,2
362 FORI=0TO4:PRINT"a";I;:INPUTA(I+1):NEXTI
364 CONSOLE1,3:CLS:INPUT"ï";DT:CLS:PRINT"Entre Coef. Dcl.":CONSOLE2,2
366 FORI=0TO4:PRINT"d";I;:INPUTD(I+1):NEXTI
368 CONSOLE1,3:CLS:INPUT"÷";PP:CONSOLE0,4:CLS
370 FORI=1TO5:PRINT£3,A(I),D(I):NEXTI
372 PRINT£3,DT,PP:GOTO276
374 '
376 CLS:PRINT"1 Estime","2 Tableau de route","3 Date d'arrive"
378 PRINT"4 MENU     ";:INPUT"choix";A1:ONA1GOTO688,384,468,28
380 GOTO376
382 '
384 CLS:PRINT"1 Entre des points","2 Modifications","3 Imp. tableau"
386 PRINT"4 MENU     ";:INPUT"choix";A:ONAGOTO392,652,418,376
388 GOTO384
390 '
392 INIT£4,"ROUTE",1000,"D":CLEAR400
394 DIMPT$(40),FD(40),FM(40),PF$(40),GD(40),GM(40),PG$(40)
396 CLS:INPUT"Port de dpart (en ttes lettres):";D$:INPUT"Port d'arrive:";PA$
398 CLS:PRINT"Entre des points":N=0:IFINKEY$=""THEN398
400 CLS:N=N+1:INPUT"Nom du point(en tteslettres):";PT$(N):CLS:PRINTPT$(N)
402 CONSOLE1,3
404 INPUT"Latitude :,',pàle (--,--.-,-)";FD(N),FM(N),PF$(N)
406 CLS:INPUT"Longitude G:,',pàle (---,--.-,-)";GD(N),GM(N),PG$(N):CONSOLE0,4
408 CLS:INPUT"Un autre point (O/N)";X$:IFX$="O"THEN400
410 PRINT£4,N,D$,PA$
412 FORI=1TON:PRINT£4,PT$(I),FD(I),FM(I),PF$(I),GD(I),GM(I),PG$(I):NEXTI
414 GOTO384
416 '
418 CLS:INIT£4,"ROUTE":CLEAR400:INPUT£4,N,D$,PA$
420 DIMPT$(N),FD(N),FM(N),PF$(N),GD(N),GM(N),PG$(N),FI(N),G(N),LC(N),RV(N)
422 DIMD(N),DP(N),RAP(N)
424 FORI=1TON:INPUT£4,PT$(I),FD(I),FM(I),PF$(I),GD(I),GM(I),PG$(I):NEXTI
426 GOSUB612
428 LPRINT°1,1§DATE$,TIME$:LPRINT°5,0§:LPRINT"   ££ ROUTE ££":LPRINT°2,1§
430 LPRINT"         ";D$;" --> ";PA$:LPRINT:LPRINTCHR$(18),"M0,0":LPRINT"I,C0"
432 LPRINT"D80,0,80,-20,160,-20,160,0,240,0,240,-20,300,-20,300,0"
434 LPRINT"D360,0,360,-20,420,-20,420,0,480,0,480,-20,0,-20"
436 LPRINT"C2,S0":LPRINT"M20,-15"
438 LPRINT"PPoint       Latitude      Longitude    Route "
440 LPRINT"P   Distance Parcourue   R.A.P":LPRINT"M0,-25"
442 LPRINT"I":LPRINT"A"
444 FORI=1TON
446 LPRINT°1,1§"                                           ";
448 LPRINTUSING"£££.£";RV(I);:LPRINTUSING"   £££££.£";D(I)
450 LPRINT°1,3§USING"  &         &";PT$(I);:LPRINT°1,1§USING"   ££";FD(I);
452 LPRINTUSING" ££.£";FM(I);:LPRINTUSING" &&";PF$(I);
454 LPRINTUSING"  £££";GD(I);:LPRINTUSING" ££.£";GM(I);
456 LPRINTUSING" &&";PG$(I);:LPRINT"                    ";
458 LPRINTUSING"  £££££.£";DP(I);:LPRINTUSING"   £££££.£";RAP(I)
460 NEXTI
462 LPRINTCHR$(18),"C0,D0,"+STR$(38+24*N)+",480,"+STR$(38+24*N)+",480,0,0,0"
464 LPRINT"A":GOTO384
466 '
468 CLS:PRINT"Date d'arrive":CONSOLE1,3:CLS
470 CLS:INIT£4,"ROUTE":CLEAR400:INPUT£4,N,D$,PA$
472 DIMPT$(N),FD(N),FM(N),PF$(N),GD(N),GM(N),PG$(N),FI(N),G(N),LC(N),RV(N)
474 DIMD(N),DP(N),RAP(N)
476 FORI=1TON:INPUT£4,PT$(I),FD(I),FM(I),PF$(I),GD(I),GM(I),PG$(I):NEXTI
478 GOSUB612
480 CLS:INPUT"Date:j,m,anne      (--,--,----)";JX,MX,AX:J=JX:MO=MX:AN=AX:CLS
482 GOSUB258:INPUT"Heure bord: heure   ronde (--)";HL
484 CLS:INPUT"Dcalage horaire (+ si-->E, - si-->W)";DA
486 CLS:PRINT"Position actuelle":CONSOLE2,2
488 INPUT"e:,',pàle (--,--.-,-)";D,MN,P$:CLS:GOSUB206:FI=D
490 INPUT"Ge:,',pàle (---,--.-,-)";D,MN,P$:CONSOLE1,3:CLS:GOSUB206:G=D
492 INPUT"Nom du prochain     point de route (en  toutes lettres):";PP$:CLS
494 LC=LOG(TAN(FI/2+PI/4)):K=0
496 K=K+1
498 IFPT$(K)=PP$THEN502
500 IFK=N+1THEN492ELSE496
502 INPUT"Vitesse minimale escompte (en Nds)";V1
504 CLS:INPUT"Vitesse maximale escompte (en Nds)";V2
506 CLS:INPUT"Ecart dsir entre  chaque vitesse (en Nds)";EC:CLS
508 DATA1,2,3,4,5,6,7,8,9,10,11,12,13,14
510 DATA31,28,31,30,31,30,31,31,30,31,30,31,31,28
512 FORI=0TO13:READNM(I):NEXTI
514 FORI=0TO13:READJR(I):NEXTI
516 IFAX/4=INT(AX/4)THENJR(1)=JR(1)+1
518 IF(AX+1)/4=INT((AX+1)/4)THENJR(13)=JR(13)+1
520 G1=ABS(G(K)-G):IFG1>PITHENG1=(2*PI)-G1
522 R=ATN(G1/(LC(K)-LC)):M=ABS((FI(K)-FI)*RD*60/COS(R))
524 DP=DP(K)-M:RAP=RAP(K)+M
526 '
528 LPRINT°1,1§DATE$,TIME$:LPRINT°4,0§:LPRINT" <<DATE D'ARRIVEE>>"
530 LPRINT°2,1§"      "; D$;" --> ";PA$:LPRINT:LPRINT"      Date: ";A$;J;MO;AN
532 LPRINT:LPRINT°2,2§USING"Distance R.A.P:£££££.£ milles";RAP
534 PRINTUSING"RAP:£££££.£ milles";RAP
536 PRINTUSING"DP:£££££.£";DP
538 LPRINTUSING"Distance parcourue:£££££.£ milles";DP
540 IFINKEY$=""THEN540
542 FORX=V1TOV2STEPEC:J=JX:MO=MX:AN=AX
544 J1=RAP/X/24:J2=CINT(J1):H=(J1-J2)*24:H2=CINT(H):MS=CINT((H-H2)*60)
546 H=H2+HL+DA
548 IFH<24THEN552
550 H=H-24:J=J+1
552 J=J+J2:T=JA-J
554 I=0
556 IFMO=NM(I)THEN560ELSE558
558 I=I+1:GOTO556
560 IFJ>JR(I)THENJ=J-JR(I):MO=MO+1:GOTO556
562 IFMO>12THENMO=MO-12:AN=AX+1
564 CONSOLE0,4
566 GOSUB262:LPRINT:LPRINT"Arrivee le:";A$;J;MO;AN;H;"h";MS"mn"
568 CLS:PRINTUSING"Pour Vm= ££.££ Nds";X;
570 PRINT"  Arrive: ";A$;J;MO;AN;H;"h";MS"mn";
572 LPRINTUSING"Pour Vm= ££.££ Nds";X
574 IF INKEY$=""THEN574
576 CONSOLE0,4:CLS
578 IFX$="O"THEN582
580 NEXTX
582 CLS:INPUT"Une date d'arrive  (O/N)";X$:CLS:LPRINT:IFX$<>"O"THEN376
584 INPUT"Date d'arrive:j,m,anne,h,mn (--,--,----,--,--)";J,MO,AN,H,MS
586 TA=J+(365.25*AN):I=0
588 CLS:PRINT"Date d'arrive":CONSOLE1,3:LPRINT°,1§"Recherche de vitesse"
590 IFMO=1THEN596
592 TA=TA+JR(I):I=I+1
594 IFMO=NM(I)THEN596ELSE592
596 TA=(TA*24)+H+(MS/60)
598 TB=JX+(365.25*AX):I=0
600 IFMX=1THEN606
602 TB=TB+JR(I):I=I+1
604 IFMX=NM(I)THEN606ELSE602
606 TB=(TB*24)+HL+DA
608 X=RAP/(TA-TB):GOTO566
610 '
612 PI=4*ATN(1):RD=180/PI:DIMNM(13),JR(13)
614 FORI=1TON:D=FD(I):MN=FM(I):P$=PF$(I):GOSUB206:FI(I)=D:NEXTI
616 FORI=1TON:D=GD(I):MN=GM(I):P$=PG$(I):GOSUB206:G(I)=D:NEXTI
618 FORI=1TON:LC(I)=LOG(TAN(FI(I)/2+PI/4)):NEXTI
620 FORI=2TON
622 G=(ABS(G(I)-G(I-1)))*RD:IFG>180THENG=360-G
624 IFFI(I)=FI(I-1)THENRV(I)=90ELSERV(I)=RD*ATN((G/RD)/(LC(I)-LC(I-1)))
630 G=(ABS(G(I)-G(I-1)))*RD:IFG>180THENRV(I)=360-RV(I):G=360-G
632 IFFI(I-1)>FI(I)THENRV(I)=RV(I)+180
633 IFG(I)<G(I-1)THENRV(I)=360-RV(I)
634 IFRV(I)<0THENRV(I)=RV(I)+360
636 IFRV(I)>360THENRV(I)=RV(I)-360
638 D(I)=ABS(((FI(I)-FI(I-1))*RD*60)/COS(RV(I)/RD))
640 IFABS(COS(RV(I)/RD))<COS(89/RD)THEN641ELSE642
641 D(I)=ABS(60*G*COS((FI(I)+FI(I-1))/2)/SIN(RV(I)/RD))
642 NEXTI
644 FORI=1TON:DP(I)=DP(I-1)+D(I):NEXTI
646 FORI=1TON:RAP(I)=DP(N)-DP(I):NEXTI
648 RETURN
650 '
652 CLS:INIT£4,"ROUTE":CLEAR400:INPUT£4,N,D$,PA$
654 DIMPT$(40),FD(40),FM(40),PF$(40),GD(40),GM(40),PG$(40)
656 FORI=1TON:INPUT£4,PT$(I),FD(I),FM(I),PF$(I),GD(I),GM(I),PG$(I):NEXTI
658 '
660 CLS:PRINT"1 Changement Coord.","2 Suppression point","3 Ajout point"
662 PRINT"4 MENU     ";:INPUT"choix";A:ONAGOTO668,850,870,384
664 GOTO660
666 '
668 CLS:INPUT"Nom (en ttes lettresdu point ‰ corriger:";PP$:I=0
670 I=I+1
672 IFPT$(I)=PP$THEN674ELSE670
674 CLS:PRINTPT$(I):CONSOLE1,3:PRINT"Latitude:"FD(I);FM(I);PF$(I)
676 INPUT"Nouvelle latitudez";FD(I),FM(I),PF$(I)
678 PRINT"Longitude:"GD(I);GM(I);PG$(I)
680 INPUT"Nouvelle longitude:";GD(I),GM(I),PG$(I)
682 CONSOLE0,4:CLS:INPUT"Une autre correction (O/N)";X$
684 IFX$="O"THEN668ELSE410
686 '
688 CLS:PRINT"ESTIME":CONSOLE1,3:PI=4*ATN(1):RD=180/PI
689 DEFFNA(X)=-ATN(X/SQR(1-X^2))+PI/2
690 INPUT"Vitesse du bateau:enNds";V:INPUT"Dclinaison:,pàle  (--.-,-)";DC,P$
692 IFP$="W"THENDC=-DC
694 INPUT"Drive due au vent: ,B ou T (--,-)";DR,P$
696 IFP$="B"THENDR=-DR
698 INPUT"Direction du courant (---)";DI:INPUT"Vitesse du courant: Nds(--)";VC
700 CLS:PRINT"1 Route fond","2 Cap ‰ suivre":INPUT"choix:";A:ONAGOTO704,722
702 GOTO700
704 INPUT"Cap compas:";CC:INPUT"Dviation compas:";DC:RS=(CC+DC+DV+DR)/RD
706 GOSUB750
716 PRINTUSING"Route fond:£££.£";RF:PRINTUSING"Vitesse:££.£ Nds";VF
718 IFINKEY$=""THEN718
720 CONSOLE0,4:GOTO376
722 CLS:INPUT"Route fond:(---.-)";R1
724 CLS:Z=VC/V*SIN((DI-R1)/RD)
728 RS=ABS((ATN(Z/SQR(1-Z^2)))-(R1/RD))
730 CV=(RS*RD)-DC-DR:GOSUB750
732 IFCSNG(RF)<>R1THENCV=180-CV
734 IFCV<0THENCV=360+CV
736 PRINTUSING"Cap compas:£££.£";CV
738 IFINKEY$=""THEN738
740 CONSOLE0,4:GOTO376
750 X1=V*COS(RS):Y1=V*SIN(RS):X2=VC*COS(DI/RD):Y2=VC*SIN(DI/RD)
752 X=X1+X2:Y=Y1+Y2:VF=SQR(X^2+Y^2)
754 IFY>=0THENRF=FNA(X/VF):GOTO758
756 RF=ATN(Y/X)
757 IFX<0THENRF=PI+RF
758 CLS:RF=RF*RD:IFRF<0THENRF=RF+360
759 RETURN
804 '
806 PRINT"DROITE DE SOLEIL":CONSOLE1,3
808 CLS:INPUT"Elvation de l'oeil en m (--.-):";MT:CLS
810 INPUT"Heure TU de l'observation(h,mn,s)";HO,MM,S:HO=HO+(MM/60)+(S/3600)
812 CLS:GOSUB828
814 P1=AH+G:IFP1>PITHENP=(2*PI)-P1ELSEP=P1
816 INPUT"Hauteur observe:,' (--,--.-)";D,MN:CLS:GOSUB206:HV=D*RD
818 HV=(HV-((1.78*SQR(MT))+TAN((90-HV)/RD))/60)+(PP*COS(HV/RD))+DT
820 GOSUB236:IT=(HV-HE)*60
822 PRINTUSING"Azimut £££.£";AZ:PRINTUSING"Inters. £££.£";IT
824 IFINKEY$=""THEN824ELSE58
826 '
828 INIT£3,"SOLEIL"
830 FORI=1TO5:INPUT£3,A(I),D(I):NEXTI
832 INPUT£3,DT,PP
834 T=(2*(J+(HO/24))/33)-1
836 M=-ATN(T/SQR(1-T^2))+PI/2:AH=0:DC=0
838 FORK=1TOu:AH=AH+A(I)*COS((I-1)*M):DC=DC+D(I)*COS((I-1)*M):NEXTI
840 IFAH<0THEN842ELSE844
842 AH=AH+360:GOTO840
844 AH=AH/RD:DC=DC/RD
846 RETURN
848 '
850 CLS:PRINT"Suppression de Point":CONSOLE1,3
852 CLS:INPUT"Nom du point ‰      supprimer:";PP$:J=0
854 J=J+1:IFJ=N+1THEN852
856 IFPT$(J)=PP$THEN858ELSE854
858 FORI=JTON:PT$(I)=PT$(I+1):FD(I)=FD(I+1):FM(I)=FM(I+1):NEXTI
860 FORI=JTON:PF$(I)=PF$(I+1):GD(I)=GD(I+1):GM(I)=GM(I+1):NEXTI
862 FORI=JTON:PG$(I)=PG$(I+1):NEXTI
864 CONSOLE0,4:N=N-1:CLS:INPUT"Une autre suppression (O/N)";X$
866 IFX$="O"THEN850ELSE410
868 '
870 CLS:PRINT"Ajout de Point":CONSOLE1,3
872 CLS:PRINT"1 Point interm.     2 Poursuite voy."
874 INPUT"choix:";A:ONAGOTO876,400
876 CLS:INPUT"Nom du point suivantcelui ‰ ajouter:";PP$:J=0
878 J=J+1
880 IFPT$(J)=PP$THEN884ELSE882
882 IFJ=NTHEN876ELSE878
884 FORI=NTOJSTEP-1:PT$(I+1)=PT$(I):FD(I+1)=FD(I):FM(I+1)=FM(I):NEXTI
886 FORI=NTOJSTEP-1:PF$(I+1)=PF$(I):GD(I+1)=GD(I):GM(I+1)=GM(I):NEXTI
888 FORI=NTOJSTEP-1:PG$(I+1)=PG$(I):NEXTI
890 CLS:INPUT"Nom du point ‰      ajouter:";PT$(J)
892 INPUT"Latitude :,',pàle (--,--.-,-)";FD(J),FM(J),PF$(J)
894 CLS:INPUT"Longitude G:,',pàle (---,--.-,-)";GD(J),GM(J),PG$(J):CONSOLE0,4
896 N=N+1:GOTO410
