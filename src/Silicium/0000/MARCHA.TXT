
10 REM"MARCHAND DE L'ESPACE"
20 DEFINTI,N:R=RND(0)
30 DIMP(9,10),A$(16)
40 DEFFNA(A,B,C,D)=ATN((D-B)/(C-A))*180/3.141592653589806£
50 DEFFNK(A,B,C,D)=SQR((C-A)^2+(D-B)^2)
60 DEFFNR(A)=A/180*3.141592653589806£
70 DEFFNS(A,B)=A*(.7-.4*(ATN(B/40-1)))
80 FORI=1TO9
90 FORJ=0TO10
100 READP(I,J)
110 NEXTJ,I
120 DATA57,.24,1,0,0,90,40,50,30,40,40
130 DATA108,.62,2,0,0,100,20,40,10,10,50
140 DATA150,1,2,0,0,100,30,40,20,10,70
150 DATA228,1.88,1,0,0,100,30,30,20,20,60
160 DATA778,11.86,28,0,0,110,10,50,10,20,60
170 DATA1426,29.46,24,0,0,90,10,50,10,20,70
180 DATA2871,84.08,10,0,0,90,40,40,10,30,40
190 DATA4497,164.82,10,0,0,80,30,40,10,30,80
200 DATA5914,248.54,8,0,0,90,40,30,20,40,60
210 FORI=0TO16
220 READA$(I):NEXT
230 DATA"VOTRE ","MERCURE","VENUS","TERRE","MARS"
240 DATA"JUPITER","SATURNE","URANUS","NEPTUNE","PLUTON"
250 DATA"ESPACE","OR","CHARBON","FER","HYDROGENE","OXYGENE","URANIUM"
260 FORI=1TO9
270 P(I,3)=RND(1)*6.283185307179603£
280 NEXT
290 P(0,3)=P(3,3)
300 FORI=5TO10
310 READP(0,I):NEXT
320 DATA10,30,20,40,40,20
330 M=4700:E=50000!
340 REM "DEPART"
350 CLS:P=3
360 PRINT"ACHAT PARTICULIER ?"
370 Y$=INKEY$:IFY$=""THEN370
380 IFY$<>"Y"THEN410
390 FORI=5TO10:P(0,I)=0:NEXT
400 M=10000:GOSUB1510:GOSUB1610
410 D=D+1:GOSUB1010
420 X1=X:Y1=Y
430 REM "ESPACE"
440 CLS:GOSUB1410
450 PRINT" PLANETE(1-9)?"  ;
460 N=INSTR("0123456789",INKEY$)-1
470 IFN<=0THEN460
480 PRINT:P=N:D=D+1:GOSUB1010
490 PRINTA$(N);" DONT LES COORDONNEES SONT",INT(X);" ";INT(Y)
500 PRINT"1=ORDINATEUR:2=MAN:3=AUTRE ?";
510 ONVAL(INKEY$)GOTO540,660,450
520 GOTO510
530 REM "ORDINATEUR"
540 PRINT"   CALCUL DE LA ROUTE POUR ";A$(P)
550 FORI=1TO24
560 BEEPI,1:NEXT
570 IFX1>XTHEN610
580 IFY1>YTHEN600
590 C1=FNA(X1,Y1,X,Y):GOTO620
600 C1=FNA(X1,Y1,X,Y)+360:GOTO620
610 C1=FNA(X1,Y1,X,Y)+180
620 K=FNK(X1,Y1,X,Y):R=RND(1)^6
630 IFK<10000THEN690
640 BEEP4,10:PRINT"TROP LOIN/ CALCULER MANUELLEMENT!"
650 REM "MANUEL"
660 PRINT"   NAVIGATION MANUELLE"
670 INPUT"DIRECTION (0-360) ";C1
680 INPUT"DISTANCE =";K:R=0
690 C=FNR(C1)
700 REM "COURBURE"
710 PRINT"DIRECTION =";C1
720 PRINT"DISTANCE =";K
730 INPUT"O.K ";Y$
740 IFY$<>"Y"THEN450
750 PRINT"COURBURE !"
760 BEEP10,20
770 E=E-100-K^2/10000000£
780 X1=X1+COS(C)*K*(RND(1)*R+1)
790 Y1=Y1+SIN(C)*K*(RND(1)*R+1)
800 GOSUB1060:GOSUB1110
810 IFP=10THENGOSUB1410:GOTO450
820 PX=0:PY=0:CLS
830 GOSUB1010
840 XD=60+(X-X1)/20:YD=16-(Y-Y1)/20
850 CIRCLE(XD,YD),P(P,2)
860 GOSUB1210:BEEP15,10:CLS
870 PRINT"ATTERRI SUR ";A$(P)
880 IFP(P,4)=1THENPRINT"DEJA VISITEE !":BEEP2,10:GOTO440
890 P(P,4)=1:IFP=3THEN2010
900 IFINKEY$=""THEN900
910 PRINT"VENDRE":GOSUB1510:GOSUB1810
920 PRINT"ACHETER":GOSUB1610
930 PRINT"DEPART!":BEEP5,5
940 GOTO440
1000 REM "CALC X,Y"
1010 R=6.283185307179605£/P(P,1)/365.242*D+P(P,3)
1020 X=COS(R)*P(P,0)*100
1030 Y=SIN(R)*P(P,0)*100
1040 RETURN
1050 REM "VERIFICATION"
1060 IFE<0THENPRINT"PLUS D'ENERGIE":GOTO2210
1070 IFX1^2+Y1^2<20000THENPRINT"COLLISION AVEC LE SOLEIL":GOTO2210
1080 RETURN
1100 REM "ATTERRISSAGE"
1110 FORI=1TO9
1120 P=I:GOSUB1010
1130 IFABS(X-X1)<1200ANDABS(Y-Y1)<320THENI=20
1140 NEXT
1150 IFI<11THENP=10
1160 RETURN
1200 REM "ATTERRI"
1210 XP=RND(1)*120:YP=RND(1)*32
1220 IFFNK(XD,YD,XP,YP)<P(P,2)THEN1210
1230 PSET(XP,YP):I$=INKEY$
1240 PX=PX-(I$=CHR$(128)):PX=PX+(I$=CHR$(29))
1250 PY=PY+(I$=CHR$(39)):PY=PY-(I$=CHR$(31))
1260 E=E+20*(I$>CHR$(27)ANDI$<CHR$(32))
1270 PRESET(XP,YP)
1280 XP=XP+PX:YP=YP+PY
1290 IFXP<0THENXP=0ELSEIFXP>119THENXP=119
1300 IFYP<0THENYP=0ELSEIFYP>31THENYP=31
1310 IFFNK(XD,YD,XP,YP)<P(P,2)THENRETURN
1320 BEEP5,1:GOTO1230
1400 REM "AFFICHAGE"
1410 PRINT"M=";MID$(STR$(M),2);
1420 PRINT" E=";MID$(STR$(INT(E)),2);
1430 PRINT" ICI ";INT(X1);INT(Y1)
1440 PRINTA$(P);".";:RETURN
1500 REM "PRIX DU MARCHE"
1510 FORI=5TO10
1520 P(P,I)=INT(P(P,I)*(RND(1)*.4+.8)):NEXT
1530 RETURN
1600 REM "ACHETER"
1610 PRINTA$(0);"ARGENT=";M
1620 FORI=5TO10
1630 PRINTA$(0)+A$(I+6);" DONT LE PRIX DU MARCHE EST ";P(P,I)
1650 INPUT"QUANTITEE ACHETEE ";N
1660 IFN<=0THEN1720ELSED=D+1
1670 INPUT"PRIX UNITAIRE ";W
1680 Z=FNS(P(P,I),N)
1690 IFZ>WORW*5<P(P,I)THENPRINT"VENTE IMPOSSIBLE A CE PRIX":GOTO1630
1700 IFN*W>MTHENPRINT"ARGENT=";M;"RESTANT!":GOTO1640
1710 M=M-N*W:P(0,I)=P(0,I)+N
1720 PRINT"ARGENT RESTANT=";M
1730 NEXT:RETURN
1800 REM "SALE"
1810 FORI=5TO10
1820 PRINTA$(I+6);" DONT LE PRIX DU MARCHE EST ";P(P,I)
1830 PRINT"EN MAINS=";P(0,I):INPUT" QUANTITEE VENDUE ";N
1840 IFN>P(0,I)THENPRINTA$(I+6);P(0,I);"RESTANT!":GOTO1830
1850 IFN<=0THEN1910ELSED=D+1
1860 INPUT"PRIX UNITAIRE ";W
1870 Z=(P(P,I)/W)^10
1880 IFZ<NTHENN=Z
1890 P(0,I)=P(0,I)-N
1900 M=M+N*W:PRINT"A ";W;" VENDU ";N
1910 NEXT
1920 RETURN
2000 REM "FIN"
2010 PRINT"RENTRE SUR TERRE"
2020 PRINT"BOUCLEMENT DES COMPTES"
2030 FORI=5TO10:P(P,I)=P(P,I)*.6:NEXT:GOSUB1510
2040 FORI=5TO10
2050 PRINT"SOLDE ";A$(I+6);":";P(0,I)
2060 PRINT"PRIX DU MARCHE ";P(P,I);"* EN MAIN ";P(0,I);"="
2070 PRINTP(P,I)*P(0,I):M=M+P(P,I)*P(0,I)
2080 IFINKEY$=""THEN2080
2090 NEXT:PRINT"ENERGIE RESTANTE:";INT(E);" EGAL"
2100 PRINT" CONVERTI EN ";INT(E/100):M=M+INT(E/100)
2110 IFD<180THENPRINT"RETOUR DANS LES DELAIS: BONUS DE ";180-D:M=M+180-D
2120 FORI=1TO9
2130 IFP(I,4)=1THEN2160
2140 PRINT"PAS VISITE ";A$(I)
2150 PRINT"AMENDE ";I^3*10:M=M-I^3*10:BEEP2*I,5
2160 NEXT
2170 PRINT"TOUT EST TERMINE":BEEP1,10
2180 PRINT"ARGENT =";M
2190 PRINT"JOURS =";D;"BENEFICE =";M-10000
2200 REM "TERMINE"
2210 PRINT"JEU TERMINE":END
