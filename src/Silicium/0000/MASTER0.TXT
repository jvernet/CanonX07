
1 CLEAR0:CLEAR100:CONSOLE0,4,0,1,0:CLS:' (C) LOGI'STICK 1984
2 FORI=1TO30STEP2:CIRCLE(I*2,16),I:CIRCLE(119-(I*2),16),I:NEXTI:FORI=1TO30
3 CIRCLE(60,16),I:NEXTI:A$="       LOGI'STICK PRESENTE MASTER MIND       "
4 LOCATE6,2:PRINT"        ":FORI=35TO83:PRESET(I,15):NEXTI:FORI=15TO23
5 PRESET(35,I):NEXTI:FORI=1TOLEN(A$)-6:X$=MID$(A$,I,8):LOCATE6,2:PRINTX$
6 FORK=1TO30:NEXTK:NEXTI:CLS:LOCATE1,1:PRINT"A vous de jouer..."
7 CONSOLE0,4:FORI=0TO9:READA,B:BEEPA,B:NEXTI
8 DATA333,4,333,4,333,3,248,9,248,5,220,9,220,9,164,12,195,6,245,11
10 X=RND(0):FOR I=1 TO 4
20 X(I)=INT(RND(1)*9)+1
30 FOR J=1 TO I
40 IF I=J THEN 60
50 IF X(I)=X(J) THEN 20
60 NEXT J
70 NEXT I
80 CLS :PRINT" £   * * * *  N  B":CONSOLE 1,3
90 FOR U=0 TO 9
100 PRINT U;
110 FOR I=1 TO 4
120 Y$=INKEY$:IF Y$="" THEN 120 ELSE Y(I)=VAL(Y$)
130 IF Y(I)=0 THEN BEEP 5,5:GOTO 120
140 FOR J=1 TO I
150 IF I=J THEN 180
160 IF Y(I)=Y(J) THEN BEEP 5,5:GOTO 120
170 NEXT J
180 PRINT TAB(I*2+2);Y(I);:BEEP30,1
190 NEXT I
200 X=0:Y=0:FOR I=1 TO 4
210 FOR J=1 TO 4
220 IF I=J AND X(I)=Y(J) THEN X=X+1
230 IF I<>J AND X(I)=Y(J) THEN Y=Y+1
240 NEXT J
250 NEXT I
260 IF X=4 THEN PRINT"GAGNE!":GOSUB320:GOTO295
270 IFU=9THENPRINT" PERDU!":GOSUB400:GOSUB300 :GOTO 295
280 PRINT X;Y
290 NEXT U
295 CONSOLE 0,4:CLS:LOCATE1,1:PRINT" UNE AUTRE PARTIE ?":LOCATE6,2
296 PRINT"(O)/(N)"
297 A$=INKEY$:IFA$="O"THEN10ELSEIFA$="N"THENCLS:PRINT"  Adieu...":GOSUB400:END
298 GOTO297
300 PRINT" SOLUTION : ":FORI=1TO4:PRINTX(I);:NEXTI
310 IF INKEY$="" THEN 310 ELSE RETURN
320 FORI=1TO48:BEEPI,1:NEXTI:RETURN
400 FORI=999TO4000STEP99:BEEPI,2:NEXTI:RETURN
1000 BEEP333,6:BEEP333,6:BEEP333,5:BEEP248,11:BEEP248,7:BEEP220,11:BEEP220,11
1010 BEEP164,20:BEEP195,11:BEEP245,9
