
10 GOSUB10000
15 CONSOLE0,4,0,0,1:CONSOLEà,1
17 FONT$(131)="48,48,48,48,48,48,48,48"
20 CLS:CLEAR:PI=3.1415926535898£
30 CLS:PRINT"Nbre alea.(0<n<1)?":INPUTN
40 IFN<=0ORN>=1THEN30
50 F=8:N=N-INT(LOG(N+PI))
60 G=500:B=0:X=250:Y=5
70 GOSUB5000
80 A$=INKEY$
90 IFSTRIG(1)=-1THENS=0:C=0:GOTO230
100 Q=STICK(0)
105 IFA$="O"ANDZ=HTHENV=V-12
110 H=10000*INT(X/G)+INT(Y/G)
120 H=H+N:H=H*N+PI:H=H*1000*PI
130 H=H-INT(H):H=H*H*H*H*H:H=INT(1000*H)
140 IFFIX(X/G)=0ANDFIX(Y/G)=0THENH=0
150 LOCATE3,1:PRINTUSING"££££";H;
160 IFA$="C"THENGOSUB1000:GOSUB5000:GOTO230
170 IFF=0THEN190
180 IFVAL(A$)<>0THENV=V+(VAL(A$)-6)*2:GOTO240
190 IFQ=0THEN230
200 Q=PI/4*(Q-1)
210 C=C+SGN(FIX(10*SIN(Q))/10)*5
220 S=S-SGN(FIX(COS(Q)*10)/10)/2
230 IFF=0ANDDV>65THENV=V-S-1
240 IFV>160THENV=160
250 IFV<0THENV=0
260 IFABS(C)>65THENC=65*SGN(C)
270 IFS<-9.5THENS=-9.5
280 IFS>8.5THENS=8.5
290 D=-ABS(C)/30
300 Q=S/648*PI*V
310 IFV<75ANDS<0THEN360
320 IFV>75ORV<75THEN360
330 R=-Q+D
340 V=V-.5
350 Z=Z+R:W=C*12/V:GOTO240
360 R=Q+D
370 IFV<75ANDZ-H=0THENR=0
380 IFV>75THENW=C*12/V
390 IFINT(ABS(Z-H))=0THENW=C*V/100
400 Z=Z+R
410 IFV<75ANDZ-H>0THENS=0:T=10
420 IFT>0THENV=V+5:T=T-1:S=T-9.9:R=-10:Z=Z+R:BEEP100,2:BEEP200,2:BEEP100,2
430 IFT>0ANDZ-H<0THEN2400
440 B=B+W:IFB>360THENB=B-360
450 IFB<0THENB=B+360
460 F=F-1/(180-V/2-2*R):IFF<0THENF=0
470 K=SQR(ABS((V*V)/12.96-R*R)):Q=(90-B)/180*PI
480 X=X+K*COS(Q)
490 Y=Y+K*SIN(Q)
495 IFZ-H>=5THEN580
500 IFZ-H=0ANDV<50THENS=0
510 IFZ-H<0ANDR>0THEN2500
520 IFZ=0ANDV=0THENF=8
530 IFZ-H<-2THEN2000ELSEIFZ-H<0THENZ=H:BEEP100,2:BEEP200,10
537 IFZ=HANDV>97THEN2000
540 IFZ-H=0ANDR<-1THEN2100
550 IFZ-H=0ANDS<0THEN2200
560 IFZ-H=0ANDR<0THENR=0
570 IFZ-H<ABS(SIN(C/180*PI))*5ANDV>50THEN2300
580  LOCATE3,0
590 PRINTUSING"££££";Z
600 LOCATE 10,0
610 PRINT USING"££££££";X
613 LOCATE 10,1
617 PRINT USING"££££££";Y
620 LOCATE0,3
630 IFZ-H>0THENPRINT CHR$(132);ELSEPRINTCHR$(133);
633 LOCATE 13,2
637 PRINTUSING"£££";B
640 LOCATE 1,2
650 PRINT USING"£££";V
660 LOCATE 3,3
670 PRINT USING"££.£";S;
680 LOCATE 17,1
690 IFC=0THENPRINT"-";ELSEIFC<0THENPRINT"<";ELSEPRINT">";
700 PRINTUSING"££";ABS(C);
710 LOCATE11,3
720 PRINT USING"£££.£";R;
770 LOCATE17,2
780 PRINTUSING"££";INT(F*10);
790 GOTO80
1000 CLS
1010 K=INT(X/G)
1020 L=INT(Y/G)
1030 FORI=-1TO2
1040 FORJ=-1TO2
1050 E=10000*(K+I)+L+J:E=E+N:E=E*N+PI:E=1000*E*PI
1060 E=E-INT(E)
1065 E=E*E*E*E*E
1070 E=INT(1000*E)
1080 IFK+I=0ANDL+J=0THENE=0
1090 IFK+I=-1ANDL+J=0THENE=0
1100 IFK+I=-1ANDL+J=-1THENE=0
1110 IFK+I=0ANDL+J=-1THENE=0
1120 E=ABS(E)
1130 LOCATE 5+5*I,2-J
1140 PRINTUSING"££££";E;
1150 NEXTJ,I
1160 LINE(23,15)-(55,15):LINE-(55,23):LINE-(23,23):LINE-(23,15)
1170 FORI=0TO200:NEXT
1180 RETURN
2000 FORI=0TO10:CLS:LOCATE7,1:PRINT"CRASH!!!!":NEXTI
2005 A=STICK(1)
2020 CLS:PRINT"UNE AUTRE PARTIE?":PRINT"     (O/N)"
2030 A$=INKEY$:IFA$=""THEN2030ELSEIFA$="O"THEN10ELSEIFA$<>"N"THEN2030
2035 FORI=1TO15:BEEP5*I,2:BEEP3*I,5:BEEPI+10,1:BEEPI+5,1:NEXTI
2040 CLS:PRINT:PRINT" B Y E ! B Y E ! !":FORI=1TO700:NEXT:OFF
2100 CLS:BEEP200,5:BEEP1000,12:PRINT " ATTERRISSAGE"
2110 PRINT "TROP DUR !":GOTO2005
2200 CLS:PRINT"ATTERRISSAGE SUR":PRINT"LA ROULETTE DE NEZ"
2210 FORI=1TO200:NEXTI
2220 GOTO2000
2300 CLS:PRINT" L'AILE ";
2310 IFC>0THENPRINT"DROITE"ELSEPRINT"GAUCHE"
2320 PRINT"A TOUCHE LE SOL!!"
2330 FORI=1TO300:NEXT
2340 GOTO2000
2400 CLS:PRINT"DECROCHAGE PRES DU":PRINT"SOL!!DOMMAGE...."
2410 FORI=0TO400:NEXT
2420 GOTO2000
2500 CLS:PRINT" VOUS AVEZ HEURTE":PRINT"  LA PAROI!!"
2510 FORI=0TO400:NEXTI
2520 GOTO2000
5000 CLS
5010 PRINT"Alt    m";CHR$(131);"X      ";CHR$(131);" IcSol    m";CHR$(131);
5020 PRINT"Y      ";CHR$(131)
5030 PRINT"V   km/h";CHR$(131);"Cap   ";CHR$(223);CHR$(131);"  l";
5040 PRINT" As    ";CHR$(223);CHR$(131);"Vz     m/s";
5050 LINE(98,24)-(119,24)
5060 RETURN
10000 CLS
10010 LOCATE5,0:PRINT"SIMULATEUR":LOCATE9,1:PRINT"DE":LOCATE10,2:PRINT"VOL";
10020 LINE(0,25)-(119,25):LINE-(119,28):LINE-(0,28):LINE-(0,25)
10030 FONT$(240)="0,0,0,0,172,236,168,172"
10040 FONT$(241)="0,0,0,0,88,80,80,216"
10050 FONT$(242)="0,0,0,0,116,84,84,116"
10060 FONT$(243)="0,0,0,0,156,136,8,136"
10070 FONT$(244)="0,0,0,0,116,84,84,84"
10080 FONT$(245)="0,0,0,0,148,20,20,28"
10090 FONT$(247)="0,0,0,0,108,44,104,104"
10100 FONT$(248)="0,0,0,0,28,8,8,8"
10110 FONT$(246)="0,0,0,0,108,104,72,104"
10140 FORI=1TO60
10150 IFINKEY$<>""THEN10180ELSEBEEP3*I,1:BEEP2*I,2:BEEP4*I,4
10160 NEXT
10170 FORI=268TO310:BEEPI,1:NEXT:BEEP49,4
10180 PRINT"voulez-vous ":PRINT"le mode d'emploi ?      (0/N)";
10190 A$=INKEY$:IFA$=""THEN10190ELSEIFA$="N"THENRETURNELSEIFA$<>"O"THEN10190
10195 CLS:CONSOLE0,3
10200 CLS:PRINT"ceci est un simulateur de vol aux instrument...";
10210 LOCATE10,3:FORI=248TO240STEP-1:PRINTCHR$(I);:NEXT
10220 PSET(119,31):PSET(117,31):PSET(115,31)
10230 A$=INKEY$:IFINKEY$=""THEN10230
10239 CLS
10245 A$=INKEY$:IFA$=""THEN10245
10255 A$=INKEY$:IFA$=""THEN10255
10260 CLS:PRINT"voici maintenant":PRINT"LE MODE D'EMPLOI":PRINT"du simulateur";
10265 A$=INKEY$:IFA$=""THEN10265
10275 A$=INKEY$:IFA$=""THEN10275
10280 CLS:PRINT"envergure 9m":PRINT"vitesse de decrochage:75km/h";
10285 A$=INKEY$:IFA$=""THEN10285
10290 CLS:PRINT"les touches du":PRINT"curseur equivalent":PRINT"au manche";
10295 A$=INKEY$:IFA$=""THEN10295
10300 CLS:PRINT">:a droite":PRINT"^:pousse":PRINT"<^:pousse a gauche";
10305 A$=INKEY$:IFA$=""THEN10305
10310 CLS:PRINT"manette des gaz":PRINT"represente par":PRINT"les touches 1->9";
10315 A$=INKEY$:IFA$=""THEN10315
10320 CLS:PRINT"1a5:ralentir":PRINT"6:sans objet":PRINT"7a9:accelerer";
10325 A$=INKEY$:IFA$=""THEN10325
10327 CLS:PRINT"0:freiner"
10328 A$=INKEY$:IFA$=""THEN10328
10330 CLS:PRINT"touche centrale:":PRINT"elle sert a mettre l'avion en...";
10335 A$=INKEY$:IFA$=""THEN10335
10340 CLS:PRINT"...palier":PRINT"voici la disposition du tableau de bord";
10345 A$=INKEY$:IFA$=""THEN10345
10350 CLS:PRINT"<alt>:altitude en m","<cap>:cap(";CHR$(223);")","<Vi>:en km/h";
10351 A$=INKEY$:IFA$=""THEN10351
10352 CLS:PRINTCHR$(132);":avion en vol":PRINTCHR$(133);":avion au sol";
10355 A$=INKEY$:IFA$=""THEN10355
10360 CLS:PRINT"sous<ic>se trouve l'inclinaison de l'avion en degres";
10365 A$=INKEY$:IFA$=""THEN10365
10370 CLS:PRINT"<:Virage a gauche   >:virage a droite   -:horizontalite";
10375 A$=INKEY$:IFA$=""THEN10375
10380 CLS:PRINT"<as>:assiette(";CHR$(223);")","<var>:variation";
10382 PRINT"   d'altitude(m/s)";
10385 A$=INKEY$:IFA$=""THEN10385
10390 CLS:PRINT"<sol>:hauteur du sol <X> et <Y> coordon- nes de l'avion(M)";
10395 A$=INKEY$:IFA$=""THEN10395
10400 CLS:PRINT"le nombre en bas represente   le fuel restant";
10405 A$=INKEY$:IFA$=""THEN10405
10410 CLS:PRINT" UTILISATION","   de la CARTE","===================";
10415 A$=INKEY$:IFA$=""THEN10415
10420 CLS:PRINT"pour l'obtenir,il","faudra taper (C)"
10425 A$=INKEY$:IFA$=""THEN10425
10430 CLS:PRINT"le terrain est divise en carres de 500m de cote chacun....";
10435 A$=INKEY$:IFA$=""THEN10435
10440 CLS:PRINT"la carte donne l'al- titude des carres    autour de l'avion";
10445 A$=INKEY$:IFA$=""THEN10445
10450 CLS:PRINT"le nombre entoure represente le carre survole par l'avion";
10455 A$=INKEY$:IFA$=""THEN10455
10460 CLS:PRINT"VOULEZ-VOUS REVOIR","LES REGLES (O/N)"
10465 A$=INKEY$:IFA$=""THEN10465ELSEIFA$="O"THEN10270ELSEIFA$<>"N"THEN10465
10470 RETURN
