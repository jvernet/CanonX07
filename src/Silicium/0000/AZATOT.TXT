
9 CLS:LOCATE4,1:PRINT" AZATHOTH "
10 FORZ=65TO0STEP-1:CIRCLE(59,16),Z:BEEPZ,1:NEXT:GOSUB985
13 CLS:CLEAR30:DEFINTA-Z:DEFDBLN,O
15 INPUT"NO";A:A=RND(-A):GOSUB8200
29 'CARACTERISTIQUES
30 X=RND(0)*71+120:FORB=0TO3:READC$(B):NEXT
70 FORB=0TO3:READA(B):NEXT
75 CLS:FORB=0TO3:LOCATE0,1:PRINTSTRING$(20,32)
80 LOCATE0,0:PRINTX;"% a repartir"
82 LOCATE 0,1:PRINTC$(B);A(B);"%";:INPUTY
85 IFY<0ORY>XTHEN80
86 X=X-Y:A(B)=A(B)+Y:IFX=0THENB=3
87 NEXT:IFXTHEN75ELSECO=A(0):PI=A(1):PO=A(2):DX=A(3):ERASEA,C$:CLS
160 X=60:Y=31:P=70:TIME$="0:0:0":R=1
199 '** LA COUR **
200 CLS:LINE(58,31)- (44,31):LINE-(44,0):LINE-(76,0):LINE-(76,31):LINE-(62,31)
201 LINE(64,0)-(64,10):LINE-(69,10):LINE(71,10)-(75,10)
202 FORZ=1TO9:LINE(65,Z)-(75,Z):NEXT
203 FORZ=2TO10STEP2:FORB=46-(ZMOD4)TO62-(ZMOD4)STEP4:PSET(B,Z):NEXT:NEXT
204 PSET(58,30):PSET(X,Y)
205 GOSUB990:I=45+INT(RND(1)*2)*30:J=RND(1)*18+12:PSET(I,J)
210 A=STICK(0):IFA=1ORA=5THENF=0:G=-1
211 IFY=31ANDW=2THENCLS:BEEP48,20:PRINT"BRAVO!","VOUS ETES SAUVES!":END
212 IFY=31ANDWTHENCLS:PRINT"VOUS ETES SAUF...","MAIS VEUF!":BEEP8000,40:END
213 IFA=2ORA=6THENF=1:G=-1
214 IFA=3ORA=7THENF=1:G=0
215 IFA=4ORA=8THENF=1:G=1
216 IFA>4THENF=-F:G=-G
217 IFA>3ANDA<7ANDY=31 THENG=0
218 IFPOINT(X+F,Y+G)THENF=0:G=0
219 PRESET(X,Y):X=X+F:Y=Y+G:PSET(X,Y):IFX=70ANDY=10THENK=1:GOTO600
220 IFY<11THEN400
230 PRESET(I,J):I=I+SGN(X-I):IFG>28ANDY>JTHEN240ELSEJ=J+SGN(Y-J)
240 IFX=IANDY=JTHENU=40:H=20:L=2:N$="UN CHIEN!":GOSUB7000:GOTO200
300 PSET(I,J):GOTO210
389 ' LE SOUTERRAIN
390 CLS:LOCATE3,1:PRINT"VS EMPRUNTEZ","  UN SOUTERRAIN":GOSUB985
395 GOSUB985:GOSUB985:RETURN
399  '**LE  JARDIN**
400 CLS:LOCATE0,1:PRINT" LE  JARDIN  "
401 GOSUB985:IFRND(1)<.THENL=1:U=55:H=30:N$="UN  MONSTRE!":GOSUB7000
410 GOSUB985:CLS:LOCATE6,1:PRINT"°P§ARTIR"
420 IFD6THENLOCATE4,2:PRINT"°S§OUTERRAIN"ELSELOCATE5,2:PRINT"°C§HERCHER"
430 IFTKEY("P")THENX=RND(0)*19+45:Y=11:CLS:GOTO200
440 IFTKEY("S")ANDD6THENGOSUB390:K=SO:GOTO600
450 IFTKEY("C")ANDD6=0THEN470
460 GOTO430
470 IFRND(1)*100<DXTHEND6=1:D$(SO)=D$(SO)+" B"
480 IFD6THENCLS:LOCATE0,1:PRINT" UNE TRAPPE!":GOTO401
490 CLS:LOCATE3,1:PRINT" il n'y a rien":GOTO401
599 '**LA MAISON**
600 CLS:D1=0:LOCATE0,1:PRINT" LA MAISON ":GOSUB985
610 CLS:LOCATE2,2:PRINT"Vous etes dans..."
615 GOSUB985
620 CLS:PRINTTAB(2);S$(K):IFK=12THEN640
622 IFWANDTTHENCLS:PRINT"LA MAISON S'ECROULE!":GOSUB985:GOSUB985:P=0:GOSUB980
623 IFRND(1)<.2THENPRINT"Un adepte est la!!":GOSUB985:GOSUB8000:GOTO615
630 IFK=TPANDD5=0ANDRND(1)*100>DX+OTHENPRINT" Un PUITS! AAAAH!"ELSE640
631 K=2:P=P-10:GOSUB980:D5=1:GOTO615
640 LOCATE0,1:PRINT"portes:";D$(K),"  °V§oir"
660 GOSUB990:IFA$="V"THENGOSUB850:GOTO620
665 IFA$="P"THENCLS:PRINTP;"pts de vie","TIME:";TIME$:GOTO615
680 IFK=SOANDA$="B"ANDD6=0THEND6=2:CLS:LOCATE4,1:PRINT" Une TRAPPE!"
681 IFD6=2THEND6=1:D$(SO)=D$(SO)+"B":GOTO615
690 IFK=SOANDA$="B"ANDD6=1THENGOSUB390:GOTO400
700 IFINSTR(D$(K),A$)=0THEN660
710 IFK=5ANDA$="H"THENK=10:GOTO610
720 IFK=10ANDA$="B"THENK=5:GOTO610
721 T=VAL(MID$(TIME$,4,2))
725 IFK=12ANDA$="B"THENGOSUB7500:GOTO610
730 IFA$="N"THENB=0
740 IFA$="S"THENB=3
750 IFA$="E"THENB=6
760 IFA$="O"THENB=9
770 O1=O(K)*10^B:O1=(O1-INT(O1))*100:O2=O(K)*10^(B+2):O2=(O2-INT(O2))*10
780 IFINT(O2)=0THEN800
790 GOSUB6000:IFD4THENO(K)=O(K)-10^(-B-3)ELSE620
800 IFB=0ORB=6THENB=B+5ELSEB=B-1
810 O2=O(O1)*10^B:O2=(O2-INT(O2))*10:IFINT(O2)THENO(O1)=O(O1)-10^(-B-1)
815 IFINT(O1)=12ANDF3=0THENCLS:PRINT"   IMPOSSIBLE!!":GOTO615
820 K=O1:GOTO610
850 CLS:B=-3
860 FORZ=1TO3:B=B+3
870 N=N(K)*10^B:Z(Z)=(N-INT(N))*10
890 ONZ(Z)GOSUB3001,3002,3003,3004,3005,3006
891 NEXT
900 LOCATE15,0:PRINT"°S§"
920 GOSUB990:IFA$="S"THENRETURN
930 A=VAL(A$):N=10^(A*3-2)
940 IFINT((N(K)*N-INT(N(K)*N))*10)THENGOSUB6000:IFD4THENN(K)=N(K)-.1/NELSE850
950 N=10^(A*3-1):CLS
960 ON(N(K)*N-INT(N(K)*N))*10GOSUB2000,2100,2200,2300,2400,2500,2600
970 GOSUB985:GOSUB980:GOTO850
979  ' *** MORT ***
980 BEEP2,3:IFP>0THENRETURNELSECLS:PRINT"DESOLE...","...Vous etes MORT!"
982 FORZ=1TO11:READA,B:BEEPA,B:NEXT:GOSUB986:END
984 ' ** PAUSES **
985 FORZ=1TO1500:NEXT:RETURN
986 FORZ=1TO600:NEXT:RETURN
990 A$=INKEY$:IFA$=""THEN990ELSERETURN
1999 ' **OBJETS**
2000 PRINTTAB(5);"Une  FIOLE!"
2010 INPUT"Boire";A$:IFA$="N"THENRETURN
2020 P=P+RND(1)*21-10:N(K)=N(K)-.1/N:RETURN
2100 IFRND(1)*100>DX-30+OTHENPRINT"AAAH!  Des FLECHES":P=P-15:GOTO2120
2110 PRINT"Des FLECHES vs...",TAB(6);"FROLENT!"
2120 N(K)=N(K)-.2/N:RETURN
2200 PRINT"Un GAZ vs paralyse!!":GOSUB985:P=P-7
2210 N(K)=N(K)-.3/N:RETURN
2300 PRINT"HORREUR!!","Un SCORPION vs PIQUE":P=1:RETURN
2400 LOCATE5,1:PRINT"Un  PASSE!":IFRTHENRETURN
2410 R=1:N(K)=N(K)-.5/N:RETURN
2500 FORZ=15TO0STEP-2:CIRCLE(59,16),Z:NEXT:LINE(0,16)-(119,16)
2510 GOSUB985:GOSUB985:GOSUB985:P=P+RND(1)*41-15:O=O+.5:GOSUB980
2520 RETURN
2600 CLS:PRINT"  Un  INSIGNE!":GOSUB985:F3=1
2610 N(K)=N(K)-.7/N:RETURN
3000 ' *MOBILIERS*
3001 PRINTZ;"Une armoire":RETURN
3002 PRINTZ;"Une table":RETURN
3003 PRINTZ;"Un coffre":RETURN
3004 PRINTZ;"Un meuble":RETURN
3005 PRINTZ;UN AUTEL":RETURN
3006 PRINTZ;"Un pentacle":RETURN
5999 ' *FERMETURES*
6000 CLS:D4=0:LOCATE2,1:PRINT"C'est ferme...":GOSUB986:IFR=0THENRETURN
6010 CLS:LOCATE6,1:PRINT"°F§orcer",TAB(6);"°P§artir"
6020 IFTKEY("F")THEN6040
6030 IFTKEY("P")THENRETURNELSE6020
6040 IFRND(0)<.15THENCLS:PRINTTAB(7);"CRACK!":GOSUB985:R=0:GOTO6000
6050 IFRND(0)*100<DX-10+OTHEND4=1:RETURN
6060 CLS:PRINT"Ca resiste!!":GOSUB986:GOTO6010
6999 ' ** COMBATS **
7000 CLS:PRINTTAB(4);N$
7040 FORZ=1TO2:LOCATE0,Z:PRINTSTRING$(18,32);:NEXT
7050 GOSUB985:LOCATE0,1:IFL=1THENPRINT"Griffes:";:E=13:GOTO7100
7055 IFLTHENPRINT"Crocs:";:E=7:GOTO7100
7060 IFRND(1)<.5THENPRINT"Tchac!:";:U=X:E=6:GOTO7100
7070 PRINT"PAN!:";:U=V:E=12
7100 IFRND(1)*100>UTHENPRINT"Rate!":GOTO7200
7150 Z=RND(1)*E+1:PRINT"OUILLE!";Z:P=P-Z:GOSUB980
7200 LOCATE0,2:INPUTA$:IFA$="C"ANDC=0ORA$="F"AND(S=0ORQ=0)THEN7200ELSELOCATE0,3
7205 IFA$<>"F"ANDA$<>"C"ANDA$<>"P"THEN7200
7210 IFA$="P"THENV=PO+O:E=6+O:PRINT"OUCH!:";
7220 IFA$="C"THENV=CO+O:E=9+O:PRINT"ZIP!:";:GOSUB7400:IFC=OTHEN7040
7230 IFA$="F"THENV=PI+O:E=14+O:PRINT"PAW!:";:Q=Q-1:GOSUB7450:IFS=0THEN7040
7240 IFRND(1)*100>VTHENPRINT"rate!":GOTO7040
7250 Z=RND(1)*E+1:PRINT"Touche!";Z:H=H-Z:BEEP48,3:IFH<1THEN7300ELSE7040
7300 CLS:PRINT"Il est mort!":O=O+1:L=0:GOSUB985:RETURN
7400 IFRND(1)<.05THENPRINT"KRAC!":C=0
7401 RETURN
7450 IFRND(1)<.1THENPRINT"BAOUM!":S=0
7451 RETURN
7499 ' * AZATHOTH *
7500 CLS:LOCATE6,1:PRINT"UNE CAVE":GOSUB985:GOSUB985:CLS
7505 IFWTHENRETURN
7510 IFT>4THENPRINT"Votre femme est...",TAB(7);"MORTE!":GOTO7530
7520 PRINT"Votre femme va etre",TAB(5);"SACRIFIEE!"
7530 PRINT"AZATHOTH est la!":GOSUB985:GOSUB985:N$="  AZATHOTH"
7540 V=60:U=50:H=40:GOSUB7000:C=1:S=1:Q=Q+6:W=1
7545 IFT<5THENW=2:CLS:PRINT"Votre femme vous  accompagne!!":GOSUB985
8000 V=RND(1)*66+10:X=RND(1)*81+10:H=25:N$=" Un ADEPTE!"
8010 GOSUB7000:C=1:S=1:Q=Q+4:R=1:RETURN
8199 7 INITIALISATION
8200 DIMS$(12),D$(12),N(12),O(12):FORZ=0TO11
8205 READS$(Z),D$(Z),N(Z),O(Z)
8210 FORJ=3TO12STEP3
8220 N(Z)=N(Z)+(INT(RND(1)*2))/10^(J-1)
8240 O(Z)=O(Z)+(INT(RND(1)*2))/10^J:NEXT
8250 NEXT
8255 S$(12)="  UNE CRYPTE":D$(12)="S B":N(12)=.303606511:O(12)=6E-5
8262 A=INT(RND(1)*3+1)*3-1
8265 B=7-INT((N(Z)*10^A-INT(N(Z)*10^A))*10)
8270 N(Z)=N(Z)+B*10^(-A-1)
8280 SO=RND(1)*7:TP=RND(1)*5+7:IFTP=10THEN8280ELSERETURN
8499  ' ** PIECES **
8500 DATA"Une antichambre","N E",.405200303,.03000001
8501 DATA" Un couloir ","N O",0,.04
8502 DATA"ˆ Un temple ˆ","N",.102303501,.05
8503 DATA" Un bureau ","S E",.101205404,4E-8
8504 DATA" Un salon ","N S E O",.102205403,.06001005003
8505 DATA"Un sanctuaire","O S H",.403501305,.00002000004
8506 DATA" Une chambre ","N S",.102404301,.12004
8507 DATA" Une chambre ","N",.105401304,.09
8508 DATA"Š Un dortoir Š","E",.101403305,9E-8
8509 DATA" Un hall ","N S E O",0,.11007010008
8510 DATA"Une bibliotheque","O B",.1012044,9E-11
8511 DATA"Une chambre","S",.101404305,9E-5
8520 DATA"Couteau:","Pistolet:","Poing:","Dext.:"
8529  ' ** % BASES **
8530 DATA15,5,25,25
8599 ' ** MUSIQUE **
8600 DATA13,13,13,13,13,5,13,13,16,13,15,5,15,13,13,5,13,13,12,5,13,30
