
1 REM ANE ROUGE V.X720 le 25/11/85
2 DEFINTA-Z
4 SCREEN2,2,2:VI=&H4BA:POKEVI,1
6 GOTO26
8 PRESET(XC-4,YC+4):RETURN
10 PSET(XC-4,YC+4),CL:RETURN
12 FL=(STICK(0)+UN)/2:ONFLGOTO16,18,20,22
14 RETURN
16 IFIC=UNTHEN24ELSEIC=IC-UN:GOTO24
18 IFJC=5THEN24ELSEJC=JC+UN:GOTO24
20 IFIC=4THEN24ELSEIC=IC+UN:GOTO24
22 IFJC=UNTHEN24ELSEJC=JC-UN
24 PRESET(XC,YC):RETURN
26 DIMP(5,6),S(3,4,5):UN=1:K=0
28 T$="l'Ane Rouge "
30 CP$="N"+CHR$(223)+"du coup:"
32 P$="Choix      ":D$="Dplacement"
34 COLOR1,0:CLS:GOSUB206
36 RESTORE38:FORI=UNTO4:FORJ=UNTO5:READP(I,J):NEXTJ,I:CT=0
38 DATA 7,7,6,6,0,10,10,9,3,2,10,10,9,4,1,8,8,5,5,0
40 XI=39:YI=10:XF=163:YF=108:YP=34:DD=24
42 COLOR1,0:CLS:LOCATE7,11:PRINTT$;
44 COLOR3,0:POKEVI,1
46 LINE(XI,YI)-(XF,YI):LINE(XI,YI)-(XI,YF)
48 LINE(XI,YF)-(XF,YF):LINE(XF,YI)-(XF,YP):LINE(XF,YF)-(XF,YP+2*DD+2)
50 FORI=UNTO4:YD=12+(I-UN)*DD:YF=YD+DD-UN
52 FORJ=UNTO5:XD=40+(J-UN)*DD:XF=XD+DD-2
54 PP=P(I,J):IFPP=0THEN66
56 IFPP>8THENPP=PP-8
58 IFP(I,J)<>P(I-UN,J)THENLINE(XD,YD)-(XF,YD),PP
60 IFP(I,J)<>P(I+UN,J)THENLINE(XD,YF)-(XF,YF),PP
62 IFP(I,J)<>P(I,J-UN)THENLINE(XD,YD)-(XD,YF),PP
64 IFP(I,J)<>P(I,J+UN)THENLINE(XF,YD)-(XF,YF),PP
66 NEXTJ,I:COLOR1:POKEVI,0
68 IC=3:JC=5:CX=52:CY=20
70 LOCATE6,13:PRINTCP$;
72 LOCATE 6,14:PRINTP$;:LOCATE 16,13:PRINTCT;
74 CL=P(IC,JC):IFCL>8THENCL=CL-8
76 IFCL=0THENCL=4
78 XC=CX+(JC-UN)*DD:YC=CY+(IC-UN)*DD
80 PSET(XC,YC),CL:A$=INKEY$
82 GOSUB12
84 IFSTRIG(1)=-UNTHEN98
86 PRESET(XC,YC)
88 BEEP0,5
90 IFA$="A"THENSLEEP:SCREEN2,2,2:GOTO40
92 IFA$="S"THENGOSUB246
94 IFA$="D"THEN36
96 IFA$="R"THENGOSUB254:GOTO40ELSE74
98 IFP(IC,JC)=0THENBEEP20,3:GOTO86
100 XC=CX+(JC-UN)*DD:YC=CY+(IC-UN)*DD
102 GOSUB10
104 LOCATE6,14:PRINTD$;:I=IC:J=JC
106 GOSUB12:IFFL=0THEN106
108 PP=P(I,J):IFP(IC,JC)=PPTHEN150
110 IFP(IC,JC)<>0THEN174
112 IFPP<9THENII=I
114 IFPP<5THENJJ=J:GOTO128
116 IFPP>8THEN122
118 IFP(I,J+UN)=PPTHENJJ=J+UNELSEJJ=J-UN
120 GOTO128
122 JJ=J:IFP(I+UN,J)=PPTHENII=I+UNELSEII=I-UN
124 IFPP=9THEN128
126 IFP(I,J+UN)=PPTHENJJ=J+UNELSEJJ=J-UN
128 IFI<=IITHENIP=I:IG=IIELSEIP=II:IG=I
130 IFJ<=JJTHENJP=J:JG=JJELSEJP=JJ:JG=J
132 ONFLGOTO134,138,142,146
134 IFP(IC,J)<>0ORP(IC,JJ)<>0THEN174
136 KP=IP-UN:KG=IG-UN:LP=JP:LG=JG:GOTO176
138 IFP(I,JC)<>0ORP(II,JC)<>0THEN174
140 KP=IP:KG=IG:LP=JP+UN:LG=JG+UN:GOTO176
142 IFP(IC,J)<>0ORP(IC,JJ)<>0THEN174
144 KP=IP+UN:KG=IG+UN:LP=JP:LG=JG:GOTO176
146 IFP(I,JC)<>0ORP(II,JC)<>0THEN174
148 KP=IP:KG=IG:LP=JP-UN:LG=JG-UN:GOTO176
150 ONFLGOTO152,158,164,170
152 IFPP<9THEN174
154 IFI>2THENGOSUB8:IC=I-UN:GOTO100
156 GOTO174
158 IFPP=9ORPP<5THEN174
160 IFJ<4THENGOSUB8:JC=J+UN:GOTO100
162 GOTO174
164 IFPP<9THEN174
166 IFI<3THENGOSUB8:IC=I+UN:GOTO100
168 GOTO174
170 IFPP=9ORPP<5THEN174
172 IFJ>2THENGOSUB8:JC=J-UN:GOTO100
174 BEEP20,3:IC=I:JC=J:GOSUB8:GOTO72
176 GOSUB8:GOSUB192:GOSUB196:CT=CT+UN
178 ONFLGOTO180,182,184,186
180 P(IG,JP)=0:P(IG,JG)=0:P(KP,LP)=PP:P(KP,LG)=PP:GOTO188
182 P(IP,JP)=0:P(IG,JP)=0:P(KP,LG)=PP:P(KG,LG)=PP:GOTO188
184 P(IP,JP)=0:P(IP,JG)=0:P(KG,LP)=PP:P(KG,LG)=PP:GOTO188
186 P(IP,JG)=0:P(IG,JG)=0:P(KP,LP)=PP:P(KG,LP)=PP
188 IFP(3,5)=10ANDP(2,5)=10THEN202
190 GOTO72
192 R=40+(JP-UN)*DD:S=39+JG*DD:T=12+(IP-UN)*DD:U=11+IG*DD
194 CL=0:GOSUB200:COLOR1:RETURN
196 R=40+(LP-UN)*DD:S=39+LG*DD:T=12+(KP-UN)*DD:U=11+KG*DD
198 CL=PP:IFCL>8THENCL=CL-8
200 LINE(R,T)-(S,U),CL,B:RETURN
202 LOCATE 6,14:PRINT"Bravo !!!!!";
204 BEEP20,2:IFINKEY$=""THEN204ELSEEND
206 COLOR6,4:LINE(0,22)-(255,22):LINE(0,36)-(255,36)
208 LOCATE0,2:PRINTSTRING$(32," ");:LOCATE10,2:PRINTT$;:GOTO226
210 L=2:C=10:LG=12
212 LOCATEC,L:PRINTT$;:BEEP0,10:IFINKEY$<>""THENRETURN
214 C=C-UN:IFC>-UNTHEN212
216 X=0
218 X=X+UN:IFX>11THENC=20:GOTO212
220 G$=RIGHT$(T$,LG-X):F$=LEFT$(T$,X)
222 LOCATE0,L:PRINTG$;:LOCATE32-X,L:PRINTF$;:BEEP0,10
224 IFINKEY$=""THEN218ELSERETURN
226 LOCATE 4,6:PRINT"Faites sortir l'Šne rouge";
228 LOCATE 4,7:PRINT"(grand carr) de l'enclos";
230 LOCATE 4,8:PRINTSTRING$(25," ");:LOCATE4,9:PRINTSTRING$(25,"-");
232 LOCATE4,10:PRINT" Dplacement ->  Flches.";
234 LOCATE4,11:PRINT" Marquage    ->   <F6>.  ";
236 LOCATE4,12:PRINT" Arrt(Sleep)->    <A>.  ";
238 LOCATE4,13:PRINT" Recommencer ->    <D>.  ";
240 LOCATE4,14:PRINT" Sauvegarde  ->    <S>.  ";
242 LOCATE4,15:PRINT" Rcupration->    <R>.  ";
244 POKEVI,0:GOTO210
246 LOCATE 6,14:INPUT"SAUVE (1..3)";NS:IFNS>3ORNS<0 THEN246
248 SC(NS)=CT:FORT=1TO4
250 FORU=1TO5:S(NS,T,U)=P(T,U):NEXTU,T:BEEP25,2
252 FORZZ=1TO400:NEXT:LOCATE 6,14:PRINTP$;"      ";:RETURN
254 LOCATE 6,14:INPUT"CHARGE(1..3)";NC:IFNC>NSTHENBEEP30,2:GOTO254
256 CT=SC(NC):FORT=1TO4
258 FORU=1TO5:P(T,U)=S(NC,T,U):NEXTU,T:RETURN
