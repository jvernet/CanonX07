
1 '*************************************
2 '*           ETIQUETTE               *
3 '*           CANON X-07              *
4 '*      BOIFFIER J.M.   SEPT.85      *
5 '*************************************
39 CLEAR1000
40 DIMC(5,20),F(5,20),L$(5,20),X(5,20),Y(5,20),R(5,20)
45 DIMCC(3,5),P(3,5),A(3,5),B(3,5),K(3,5),L(3,5)
60 ONERRORGOTO5000
70 'LECTURE DU FICHIER *****************
75 CLS:PRINT"LECTURE DU FICHIER  EN COURS"
80 INIT£1,"ETI"
105 FORJ=1TO5:FORI=1TO20
115 INPUT£1,C(J,I),F(J,I),L$(J,I),X(J,I),Y(J,I),R(J,I)
120 NEXTI:NEXTJ
124 FORI=1TO3
125 FORJ=1TO5:INPUT£1,CC(I,J),P(I,J),A(I,J),B(I,J),K(I,J),L(I,J):NEXTJ
126 NEXTI
200 'MENU DEPART ***********************
205 CLS:LOCATE4,0:PRINT"* ETIQUETTE *"
210 PRINT"COMPOSITION °1§","IMPRESSION °2§":GOSUB6000
230 IFX$="1"THENGOSUB2000:GOSUB3000:GOTO200
240 IFX$="2"THENGOSUB1000:GOTO200
270 GOTO200
1000 'IMPRESSION **********************
1010 J=1:INPUT"NUMERO DE L'ETI.";J
1015 E=1:INPUT"NOMBRE D'ETI.";E:INPUT"ESPACE";L:GOSUB6010
1020 LPRINTCHR$(18):FORS=1TOE:LPRINT"I":FORI=1TO3
1021 IFA(I,J)=0ANDB(I,J)=0THEN1025ELSELPRINT"C";CC(I,J):LPRINT"L";P(I,J)
1022 LPRINT"M";K(I,J);",";-L(I,J)
1023 LPRINT"J";A(I,J);",";O:LPRINT"J";O;",";-B(I,J)
1024 LPRINT"J";-A(I,J);",";O:LPRINT"J";O;",";B(I,J)
1025 NEXTI
1030 FORI=1TO20:IFF(J,I)=0THEN1050
1035 IFR(J,I)=0THENU=1:V=0
1036 IFR(J,I)=1THENU=0:V=-1
1037 IFF(J,I)=1THENH=1
1038 IFF(J,I)>1THENH=F(J,I)+1
1040 LPRINT"S";F(J,I)-1:LPRINT"Q";R(J,I)
1041 LPRINT"M";X(J,I);",";-Y(J,I)
1042 LPRINT"C";C(J,I)
1043 FORT=1TOH
1045 LPRINT"M";X(J,I)+U*T;",";-Y(J,I)+V*T
1046 LPRINT"C";C(J,I)1042
1047 LPRINT"P";L$(J,I)
1048 NEXTT
1050 NEXTI
1065 LPRINT"M";O;",";-L
1070 NEXTS:RETURN
2000 'COMPOSITION **********************
2001 J=1:INPUT"NUMERO DE L'ETI.";J:IFJ<6THEN2003ELSE2001
2003 CLS:I=0:INPUT"CADRE";I:IFI=0THEN2015
2004 IFI<4THENGOSUB2600:GOTO2007ELSE2003
2007 GOSUB2800:LOCATE0,2:INPUT"COULEUR,PAS";CC(I,J),P(I,J)
2008 GOSUB2800:LOCATE0,2:INPUT"DIMENSION";A(I,J),B(I,J)
2009 GOSUB2800:LOCATE0,2:INPUT"ORIGINE";K(I,J),L(I,J):GOTO2003
2015 CLS:I=0
2020 INPUT"LIGNE";I:IFI=0THENRETURN
2025 GOSUB2500
2027 LOCATE5,1:INPUT"COULEUR";C(J,I):GOSUB2810
2030 LOCATE5,1:INPUT"FORMAT";F(J,I):GOSUB2810
2035 LOCATE5,1:INPUT"TAB.";X(J,I),Y(J,I):GOSUB2810
2040 LOCATE5,1:INPUT"ROTATION";R(J,I):GOSUB2810
2090 LOCATE5,1:INPUT"  COMPOSITION";L$(J,I)
2100 GOTO2015
2500 CLS:PRINTI;C(J,I);F(J,I)
2505 LOCATE10,0:PRINTX(J,I):LOCATE15,0:PRINTY(J,I):LOCATE0,1:PRINTR(J,I)
2510 LOCATE0,0:PRINT"L":LOCATE3,0:PRINT"C"
2520 LOCATE6,0:PRINT"F":LOCATE10,0:PRINT"X":LOCATE15,0:PRINT"Y"
2525 LOCATE0,1:PRINT"R"
2530 LOCATE0,2:PRINTL$(J,I)+CHR$(11):RETURN
2600 CLS:PRINTI;CC(I,J);P(I,J)
2610 LOCATE10,0:PRINTA(I,J)
2620 LOCATE15,0:PRINTB(I,J)
2630 LOCATE10,1:PRINTK(I,J)
2640 LOCATE15,1:PRINTL(I,J)
2650 LOCATE3,0:PRINT"C"
2660 LOCATE6,0:PRINT"P"
2670 LOCATE10,0:PRINT"H"
2680 LOCATE15,0:PRINT"V"
2690 LOCATE10,1:PRINT"X"
2700 LOCATE15,1:PRINT"Y"
2710 RETURN
2800 LOCATE0,2:PRINT"                   ":RETURN
2810 LOCATE5,1:PRINT"             "+CHR$(11):RETURN
3000 'ECRITURE DU FICHIER **************
3005 PRINT"SAUVEGARDE °O§/°N§":GOSUB6000
3006 IFX$="O"THEN3010
3007 IFX$="N"THENRETURN
3008 GOTO3005
3010 CLS:PRINT"ECRITURE DU FICHIER EN COURS"
3020 INIT£1,"ETI",4069,"D"
3045 FORJ=1TO5:FORI=1TO20
3050 PRINT£1,C(J,I),F(J,I),L$(J,I),X(J,I),Y(J,I),R(J,I)
3060 NEXTI:NEXTJ
3064 FORI=1TO3
3065 FORJ=1TO5:PRINT£1,CC(I,J),P(I,J),A(I,J),B(I,J),K(I,J),L(I,J):NEXTJ
3066 NEXTI
3070 RETURN
5000 'ERREUR ***************************
5005 IFERR=22ORERR=24THEN5020
5010 CLS:PRINT"ERREUR";ERR,"LIGNE";ERL:END
5020 CLS:PRINT"PAS DE FICHIER":GOSUB6020:GOSUB2000:GOSUB3000:RESUME200
6000 X$=INKEY$:IFX$=""THEN6000ELSECLS:RETURN
6010 PRINT"IMPRIMANTE OFF/ON":GOSUB6000:RETURN
6020 FORI=0TO300:NEXTI:CLS:RETURN
