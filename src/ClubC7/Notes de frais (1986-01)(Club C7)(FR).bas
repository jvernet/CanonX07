10 'NOTE DE FRAIS
20 'Copyright Y.Drillet
90 'Menu
100 CONSOLE0,4,0,1,1
120 CLEAR100:DEFINTA-N:DEFDBLW-Z:GOSUB1760
140 A=STICK(0):CLS
160 PRINT"Entrer (1)  Fin (5)","Annuler(2)","Sortir (3)","Remise a zero(4)";
180 F=0::A=VAL(INKEY$):IFA=5THENCLS:END
200 ONA*(-(A=1)-(A=2)-(A=3)-(A=4))GOSUB600,1020,1180,1680
220 IFFTHEN140ELSE180
230 'SP jour
240 CLS:INPUT"Jour";DA:IFDA<1ORDA>31THEN240
260 DA=INT(DA):RETURN
270 'SP option
280 CLS:PRINT"HOTEL(1)   REPAS(2) TRANSPORT(3)  TEL(4)";
300 PRINT"DIVERTIS.(5) ESS.(6)VOITURE(7)DIVER(8)";:A=STICK(0)
320 Y$=INKEY$:IFY$=""THEN320ELSECO=VAL(Y$):IFCO<1ORCO>8THEN320
340 RETURN
350 'SP montant
360 CLS:INPUT"MONTANT";X:IFX>999999THEN360
380 RETURN
390 'SP charge
400 W=1:INPUT"CHANGE 1FF=";W:RETURN
410 'SP lieu
420 LO$="":PRINT"Jour";DA;" Quel lieu";:INPUTLO$:LO$=LEFT$(LO$,19)
440 IFLO$=""THENF=-1:L2=0:RETURN
460 LO=LO+1:L2=LO:INIT£1,"LO"+STR$(LO),LEN(LO$)+10,"D":PRINT£1,LO$
480 RETURN
490 'SP affichage
500 CLS:D=CO+2-(CO>4)-(CO>7):RESTORE2020:FORJ=1TOD:READY$,G:NEXT
520 IFL2=0THENLO$=""ELSEINIT£1,"LO"+STR$(L2):INPUT£1,LO$
540 LOCATE 0,0:PRINT"Jour";DA;"  ";Y$;:LOCATE 0,1:PRINTLO$
560 LOCATE 0,2:PRINT"Montant";X;:LOCATE 0,3:PRINT"Change";W;
580 RETURN
590 'SP entrer
600 GOSUB240:B=0:IFN=-1THENB=-1:GOTO680
620 FORC=0TON
640 INIT£1,"EX"+STR$(C):INPUT£1,D1,C1,Z,Y,L1:IFD1=DATHENB=-1:C=N
650 IFDA<D1THENC=N
660 NEXT
670 IFL1=0THENLO$="":GOTO700
680 IFBANDL1<>0THENINIT£1,"LO"+STR$(L1):INPUT£1,LO$:L2=L1:GOTO700ELSEGOSUB420
700 GOSUB280:GOSUB360:GOSUB400
720 GOSUB500:LOCATE 15,3:PRINT"O/N";
740 Y$=INKEY$:IFY$="N"THEN600ELSEIFY$<>"O"THEN740
760 IFN=-1THENE=0:N=0:GOSUB1960:GOTO940
780 FORC=0TON
800 INIT£1,"EX"+STR$(C):INPUT£1,D1,C1,Z,Y,L1
820 IF10*DA+CO<10*D1+C1THENE=C:C=N:GOTO860
840 E=N+1
860 NEXT:N=N+1:GOSUB1960
880 FORC=NTOE+1STEP-1
900 INIT£1,"EX"+STR$(C-1):INPUT£1,D1,C1,Z,Y,L1
920 INIT£1,"EX"+STR$(C):PRINT£1,D1,C1,Z,Y,L1:NEXT
940 INIT£1,"EX"+STR$(E):PRINT£1,DA,CO,X,W,L2
960 CLS:PRINT"Nouvelle entree O/N";
980 Y$=INKEY$:IFY$=""THEN980ELSEIFY$="O"THEN600
1000 F=-1:RETURN
1010 'SP annuler
1020 CLS:INPUT"No de l'operation a annuler";E
1040 IFE>NORE<0THEN1160
1060 IFN=ETHEN1140
1080 FORC=ETON-1
1100 INIT£1,"EX"+STR$(C+1):INPUT£1,D1,C1,Z,Y,L1
1120 INIT£1,"EX"+STR$(C):PRINT£1,D1,C1,Z,Y,L1:NEXT
1140 DELETE"EX"+STR$(NO):N=N-1
1160 F=-1:RETURN
1170 REM SP sortie
1180 LPRINT°1,0§:TD=0:TT=0:L=440:K=L:O=L:L2=-1:IFN=-1THENF=-1:RETURN
1200 D1=0:CLS:PRINT"LISTE (1)","TABLEAU (2)":A=STICK(0)
1220 A=VAL(INKEY$):IF(A=1)+(A=2)=0THEN1220
1240 IFA=2THENDIMTC(8):GOSUB2080
1260 FORI=0TON:M=DA:INIT£1,"EX"+STR$(I):INPUT£1,DA,CO,X,W,L2
1280 GOSUB500
1300 IFA=2THEN1380
1320 IFD1<>DATHENLPRINT"Jour";DA"  ";LO$
1340 LPRINTI;" ";Y$;" Montant";X;"  Change";W;" ";INT(X/W*100)/100;"FF"
1360 D1=DA:NEXT:F=-1:RETURN
1380 IFK>20THEN1400
1390 GOSUB2240:LPRINT"M0,-800":GOSUB2080:L=440:K=L:O=L:M=-1:DA=0:I=I-1:GOTO1640
1400 IFM=DAANDCO=C1THENK=K-16:GOTO1500
1420 IFM=DATHENK=O:GOTO1500:ELSEL=L-16:O=L:K=L
1440 GOSUB2240
1460 LPRINT"M"+STR$(K)","+STR$(710):LPRINT"P";DA
1480 LPRINT"M"+STR$(K)","+STR$(656):LPRINT"P";LO$
1500 X=INT(100*X/W)/100
1520 LPRINT"M"+STR$(K)","+STR$(G):LPRINT"P";X
1540 TC(CO)=TC(CO)+X:TD=TD+X:TT=TT+X:C1=CO
1560 L=((L+K)-ABS(L-K))/2:NEXT:K=K-16:GOSUB2240
1580 LPRINT"M"+STR$(2)","+STR$(660):LPRINT"P";DATE$;"  TOTAUX"
1600 RESTORE2000:FORJ=1TO8:READG
1620 LPRINT"M"+STR$(2)","+STR$(G):LPRINT"P";TC(J)
1640 NEXT
1660 LPRINT"M"+STR$(2)","+STR$(-588):LPRINT"P";TT:LPRINTCHR$(17):OFF
1670 'SP remise a zero
1680 CLS:PRINT"confirmer remise a zero O/N";:INPUTY$:IFY$<>"O"THENF=-1:RETURN
1700 IFN=-1THEN1720ELSEFORI=0TON:DELETE"EX"+STR$(I):NEXT
1720 IFLO=0THEN1740ELSEFORI=1TOLO:DELETE"LO"+STR$(I):NEXT
1740 N=-1:LO=0:F=-1:RETURN
1750 'SP calcul du nombre d'operation et du nombre de lieux
1760 CLS:PRINT"UN MOMENT SVP"
1780 LO=0:N=-1:D=528:GOSUB1920:P=D
1800 IFPEEK(P)=0THENRETURN
1820 D=P+7:GOSUB1920:L=D
1840 X$=CHR$(PEEK(P))+CHR$(PEEK(P+1))+CHR$(PEEK(P+6))
1860 IFX$="EXD"THENN=N+1
1880 IFX$="LOD"THENLO=LO+1
1900 P=P+L:GOTO1800
1920 H$=HEX$(PEEK(D+1))+RIGHT$("0"+HEX$(PEEK(D)),2)
1940 D=0:FORI=1TOLEN(H$):Z=ASC(MID$(H$,I,1))-48:D=D*16+Z+7*(Z>9):NEXT:RETURN
1960 INIT£1,"EX"+STR$(N),50,"D":RETURN
1970 'Donnees
1980 DATA714,582,474,438,330,222,118,78,-30,-138,-246,-354,-660,-714
2000 DATA348,244,136,32,-124,-228,-336,-480
2020 DATADATE,710,"LIEU & REMARQUES",656,"HOTEL",350,"REPAS",242
2040 DATA"TRANSPORT",134,"TELEPHONE",30,"NO",-82,"DIVERTIS.",-122,"ESSENCE"
2060 DATA-226,"VOITURE",-334,"NO",-442,"DIVERS",-478,"MONTANT",-586
2070 'SP impression du cadre
2080 LPRINTCHR$(17):LPRINTCHR$(18);"Q1":LPRINT"D0,-714"
2100 LPRINT"I","SI"
2120 LPRINT"D0,-714,450,-714,450,714,480,714,480,-714
2140 RESTORE1980:G=0:FORT=0TO13:READJ:H=G:G=-480*(G=0)
2160 LPRINT"D"+STR$(G)","+STR$(-J)","+STR$(H)","+STR$(-J):NEXT
2180 RESTORE2020:FORT=0TO12:READY$,G:LPRINT"M452,"+STR$(G)
2200 LPRINT"P";Y$
2220 NEXT:RETURN
2230 'SP impression des sommes journalieres
2240 IFTD<>0THEN LPRINT"M"+STR$(K+16)","+STR$(-558):LPRINT"P";TD:TD=0
2280 RETURN