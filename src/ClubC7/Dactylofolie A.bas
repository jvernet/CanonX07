3 REM "PROGRAMME A"
5 CLS:CLEAR:CONSOLE0,4,0,1,0:CONSOLE@1,1,0
6 REM "PRESENTATION"
7 FORI=1TO119STEP3:LINE(I,0)-(60,16):BEEPI,1:LINE(I,31)-(60,16):NEXT
10 LOCATE0,1:PRINT"Dact":BEEP4,20:LOCATE2,2:PRINT"ylo":BEEP200,20:LOCATE15,1
15 PRINT"Folie":BEEP500,20:LOCATE16,2:PRINT"!!!":BEEP4000,20:FORG=1TO100STEP5
16 BEEPG,3:NEXT
25 REM "ATTENTE DU MOT ADVERSE"
30 CLEAR100:INIT£1,"OPT:":S=500:CLS:GOSUB300:INPUT£1,A:INPUT£1,V$
105 FORK=1TO10:BEEPK,2:NEXT:C=(20-A)/2:LOCATEC,2:PRINTSTRING$(A,"-"):E=0
110 LOCATEC,1:PRINTV$
115 REM "ENTREE DU MOT ADVERSE"
120 BEEP5,1 :G$=INKEY$:IFG$<>""THEN130
125 LOCATE15,3:PRINT"   ";:LOCATE14,3:PRINT S;:S=S-5:IFS<5THEN500ELSE120
130 IFG$<>MID$(V$,E+1,1)THEN125
135 LOCATEC,1:PRINT" ";:LOCATEC,2:PRINT" ";:C=C+1:E=E+1:IFE<ATHEN120
140 FORL=10TO1STEP-1:BEEPL,1:NEXT
145 REM "ENTREE DE VOTRE MOT"
150 C=(20-A)/2:LOCATEC,2:PRINTSTRING$(A,"-"):V$="":E=0
160 BEEP2,1:G$=INKEY$:IFG$<>""THEN177
170 LOCATE15,3:PRINT"   ";:LOCATE14,3:PRINTS;:S=S-1:IFS<0THEN500ELSE160
177 IFG$<>CHR$(13)THEN180
178 IFE<1THEN160ELSEC=C-1:E=E-1:LOCATEC,1:PRINT" ":LOCATEC,2:PRINT"-"
179 V$=MID$(V$,1,E):GOTO160
180 LOCATEC,1:PRINTG$;:V$=V$+G$:LOCATEC,2:PRINT" ";:E=E+1:C=C+1:IFE<ATHEN160
195 REM "TRANSMISSION DE CE MOT ET ATTENTE DU MOT ADVERSE"
200 FORL=1TO10:BEEPL,3:NEXT:INIT£1,"OPT:": PRINT£1,V$:INPUT£1,V$
205 IFV$="ù"THEN400ELSE105
300 LOCATE0,0:PRINTSTRING$(20,""):LOCATE0,3:PRINT"DACTYLO Points: 500";:RETURN
395 REM "GAGNE !!"
400 CLS:FORI=1TO60STEP2:CIRCLE(60,16),I:BEEPI,2:NEXT:LOCATE6,1:PRINT"GAGNE !!"
410 FORJ=1TO30STEP2:BEEPJ,4:NEXT:GOTO600
495 REM "PERDU !!"
500 INIT£1,"OPT:":V$="ù":PRINT£1,V$: CLS:FORI=60TO1STEP-2:CIRCLE(60,16),I
510 BEEP900-I,2:NEXT:LOCATE6,1:PRINT"PERDU !!":FORI=30TO1STEP-2:BEEPI,4:NEXT
595 REM "ENCORE ?"
600 CLS:BEEP9,9:PRINT"Une autre Partie....  (O)UI ou (N)ON ?"
610 G$=INKEY$:IFG$="O"THENV$="O":INIT£1,"OPT:":PRINT£1,V$:GOTO30
620 IFG$="N"THENV$="ù":INIT£1,"OPT:":PRINT£1,V$:CLS:PRINT"AU REVOIR...":END
630 GOTO610