10 CLS:CLEAR200:LPRINT[1,0];CHR$(17);CHR$(10)
20 PRINT"** Amort.Lineaire **":CONSOLE1,3
30 CLS:LINEINPUT"Element   Amorti   :";EA$:IFEA$=""THENEA$="Immobilisations"
40 CLS:LINEINPUT"Base Amortissable  :";VO$:VO=VAL(VO$):IFVO=0ORVO>1E9THEN40
50 A$=RIGHT$(VO$,3):B$=RIGHT$(VO$,2):IFB$="HT"ORA$<>"TTC"THEN100
60 CLS:LINEINPUT"Taux Tva (xx.xx %) :";T$:T=VAL(T$)
70 IFT=18.6ORT=33.33ORT=7ORT=5THEN90
80 LOCATE0,2:PRINT"[= Taux Incorrect =]";:T1=5:GOSUB500:GOTO60
90 VO=VO/(T/100+1)
100 CLS:LINEINPUT"Duree  Vie (xx ans):";N$:N=VAL(N$):NB=N:IFN<1ORN>50THEN100
110 CLS:LINEINPUT"Date d'Acquisition  ------- (jj/mm/aa) :";D$
115 IFMID$(D$,3,1)<>"/"ORMID$(D$,6,1)<>"/"THEN110
120 J=VAL(MID$(D$,1,2)):M=VAL(MID$(D$,4,2)):A=VAL(MID$(D$,7,2))
125 IFJ<1ORJ>31ORM<1ORM>12ORA=0THENPRINT"[Date erronne]":T1=4:GOSUB500:GOTO110
130 TX=100/N:AG=VO*TX/100:A1=AG:A2=AG:A3=AG:F=N-INT(N)
135 IFM=1ANDF=0THEN150ELSEIFM<>1THENA1=AG*((13-M)/12)
140 IFM=1ANDF<>0THENA3=VO-AG*INT(N):N=INT(N)+1:GOTO150
145 IFF=0THENN=N+1:A3=AG-A1ELSEA2=AG-A1:A3=VO-((INT(N)-1)*AG+A1+A2):N=INT(N)+2
150 CLS:PRINTUSING"Base: #########.# HTDuree: ## ans       ";VO;NB;
155 PRINTUSING"Date: ##/##/19##  *";J;M;A;
160 IFINKEY$=""THEN160
165 LPRINT[1,3]STRING$(79,45);:LPRINT[1,0]
170 LPRINTUSING"| Element Amorti: &                  &       ";EA$;
175 LPRINTUSING"Base Amortissable: ######### Frcs |";VO;
180 LPRINTUSING"| Duree d'Utilisation: ## ans    ";NB;
190 LPRINTUSING"Date d'Acquisition: &      &    Taux: ##.## % |";D$;TX;
195 LPRINT[1,3]STRING$(79,45);:LPRINT[1,1]
200 LPRINT"| Annees | Valeur d'Origine |    Annuites    |";
210 LPRINT"     Valeur Nette Comptable      |";
215 LPRINT[1,3]STRING$(79,45);" ";
220 FORI=1TON:LPRINT"|";STRING$(8,32);"|";STRING$(18,32);"|";STRING$(16,32);
230 LPRINT"|";STRING$(33,32);"|";:NEXT:LPRINTSTRING$(79,45);
240 FORI=1TON+1:LPRINTCHR$(11);:NEXT:LPRINT[1,0]
250 CLS:CONSOLE0,4:VN=VO:FORI=1TON:AN=AG
255 IFI=1THENAN=A1ELSEIFI=N-1THENAN=A2ELSEIFI=NTHENAN=A3
260 LOCATE0,0:PRINTUSING"Annees:         19##Val.Orig.: #########";A;VO;
270 PRINTUSING"Annuites:  #########Val.Net:  #########";AN;VN-AN;
280 LPRINTUSING"   19##     ";A;:X=VO:GOSUB600:LPRINT"    ";
290 X=AN:GOSUB600:LPRINT"           ";:VN=VN-AN:X=VN:GOSUB600
300 LPRINT"           ";:A=A+1:NEXT:LPRINT[1,0]:END
500 BEEP5,5:FORT2=1TOT1*200:NEXT:RETURN
600 X1=FIX(X/1E6):X2=FIX(X/1E3)-(X1*1E3):X3=FIX(X-(X1*1E6+X2*1E3))
610 XF=INT((X-INT(X))*100):X1$=RIGHT$(STR$(X1),3)
620 IFX1=0THENX1$=""ELSEIFX1<10THENX1$=" "+X1$
630 W=X2:Z=48:IFX1=0THENZ=32
640 GOSUB700:X2$=W$:IFX1=0ANDX2=0THENX2$=""
650 W=X3:Z=48:IFX1=0ANDX2=0THENZ=32
660 GOSUB700:X3$=W$:IFX1=0ANDX2=0ANDX3=0THENX3$=""
670 W=XF:Z=48:GOSUB700:XF$=W$
680 LPRINTUSING"& & & & & &,&&";X1$;X2$;X3$;XF$;:RETURN
700 W$=STR$(W):W$=RIGHT$(W$,LEN(W$)-1):W$=STRING$(3-LEN(W$),Z)+W$
710 RETURN