1 DEFFNA(T)=EXP(COS(T^2-3*T+1))
2 DEFFNB(T)=SIN(T/2)/(SIN(T/2-3.14/6))
3 DEFFNC(T)=(T^2+1)/(T-1):DEFFND(T)=(T^3-1)/(T^2+1)
10 REM.TRACEUR de           COURBES
20 CLS:PRINT
30 PRINT" TRACEUR de COURBES","   sur CANON X-07"
40 LINE(0,0)-(119,0):LINE(119,0)-(119,31)
42 LINE(119,31)-(0,31):LINE(0,31)-(0,0)
50 A$=INKEY$:IF A$=""THEN50 ELSE CLS
60 PRINT"-OPTIONS:°1§-Cartes"
70 PRINTTAB(9)"°2§-Polair"
80 PRINTTAB(9)"°3§-Parame"
90 B$=INKEY$:IF B$>="1" AND B$<"4" THEN O=VAL(B$)ELSE90
100 CLS
110 PRINT"-FONCTION(S)","  en ligne";O;" (O/N)?"
120 F$=INKEY$:IF F$="O"THEN130
125 IF F$="N" THEN GOSUB 5000:GOTO130
128 GOTO120
130 GOSUB3000:GOSUB4000
140 ON O GOSUB 500,1000,1500
150 REM ..Sortie...
160 CLS:LPRINT"H":LPRINT"S1":LPRINT"M-210,-380"
170 LPRINT"A":LPRINT" -Intervalle d'etude : de";A;" a ";B
180 LPRINT" -Unite :";U
190 LPRINT" -Increment :";P
200 LPRINT" -Fonction :";:GOSUB6000
210 CLS
220 LPRINT:LPRINT:LPRINT
500 REM.FCT CARTESIENNE.
510 CLS:PRINT"-Courbe en","  coordonnees","    cartesienne."
550 REM.TRACE de la courbe
560 T=A
570 LPRINT"M";T*U;",";FNA(T)*U
580 FOR T=A TO B STEP P
590 Y=FNA(T)*U:X=T*U
600 IF X<-230 OR X>230 THEN 630
610 IF Y<-350 OR Y>350 THEN 630
620 LPRINT"D";X;",";Y
630 NEXT T
640 RETURN
1000 REM Fct Polaire
1010 T=A
1020 LPRINT"M";FNB(T)*COS(T)*U;",";FNB(T)*SIN(T)*U
1030 FORT=A TO B STEP P
1040 X=FNB(T)*COS(T)*U
1050 Y=FNB(T)*SIN(T)*U
1060 IF Y<-350 OR Y>350 THEN1090
1070 IF X<-230 OR X>230 THEN1090
1080 LPRINT"D";X;",";Y
1090 NEXT T
1200 RETURN
1500 REM fct parametree...
1510 CLS:PRINT"-Courbe parametree.":T=A
1530 LPRINT"M";FNC(T)*U;",";FND(T)*U
1535 LPRINT"d"
1540 FOR T=A TO B STEP P
1550 X=FNC(T)*U
1560 Y=FND(T)*U
1570 IF X<-230 OR X>230 THEN 1600
1580 IF Y<-350 OR Y>350 THEN 1600
1590 LPRINT"D";X;",";Y
1600 NEXT T:LPRINT"f":RETURN
3000 REM.presentation      sur papier...
3002 IF O=1 THENTYPE$=" CARTESIENNE ."
3004 IF O=2 THENTYPE$=" POLAIRE ."
3006 IF O=3 THEN TYPE$=" PARAMETREE"
3010 PRINT"-Imprimante:"
3020 PRINT"-Couleur :(0 a 3)?"
3030 PRINT"   -Cadre + Axes ?"
3040 C$=INKEY$:IFC$>="0" AND C$<"4" THEN AC=VAL(C$)ELSE 3040
3050 PRINT"   -Courbe ?"
3060 D$=INKEY$:IFD$>="0" AND D$<"4" THEN OC=VAL(D$)ELSE3060
3100 REM..TRACAGE
3110 LPRINTCHR$(18):LPRINT"I":LPRINT"C";AC:LPRINT"S2"
3120 LPRINT"J480,0,0,-950,-480,0,0,950"
3130 LPRINT"M0,-45":LPRINT"J480,0":LPRINT"M0,-785"
3140 LPRINT"J480,0":LPRINT"M0,-40"
3150 LPRINT"P -FONCTION :";TYPE$
3160 LPRINT"M240,-65"
3170 LPRINT"J0,-700":LPRINT"M10,-415":LPRINT"J460,0"
3180 LPRINT"M240,-415":LPRINT"I"
3190 RETURN
4000 REM.Parametre ..
4010 PRINT"-Intervalle d'etude"
4020 IF O=1 THEN PRINT"-abcisse de (A a B)?"
4030 IF O=2 THEN PRINT"-angle de (A a B)?"
4040 IF O=3 THEN PRINT"-temps de (A a B)?"
4050 INPUT"-A=";A
4060 INPUT"-B=";B
4070 INPUT"-Unite (10 a 230)";U
4080 INPUT"-Increment (0,01 a 5)";P
4090 RETURN
5000 IF O=1THENLIST1
5010 IF O=2THENLIST2
5030 IF O=3THENLIST3
5040 RETURN
6000 IF O=1THENLLIST1
6010 IF O=2THENLLIST2
6020 IF O=3THENLLIST3
6030 RETURN