0 CLS
1 CONSOLE,,,1,0
2 CLEAR 500
3 PL=2
5 DEFINT A-B,O-Z: DEFSTR C
6 DIME(10)
9 CLS
10 LINE(0,0)-(45,0): LINE(45,0)-(45,31): LINE(45,31)-(0,31): LINE(0,31)-(0,0)
11 A=RND(0)
15 C="": L=2: M=0: P=22: Q=15: P1=22: Q1=16: B=0: A=1 : X=22: Y=12: F=-1: K=19: I=A
16 LOCATE 12,0: PRINT"POINTS:";
17 LOCATE 13,1: PRINT L;
18 LOCATE 12,2: PRINT"RECORD:";: LOCATE 13,3: PRINT PL;
19 N=0
20 IF F=-1 AND POINT(X,Y) THEN 3000
21 IF POINT(X,Y)=0 THEN 42
25 IF F=-1 THEN 42
26 H=0
30 FOR G=0 TO F
31 IF EG)<>X+Y*256 THEN 38
35 M=M+5: FOR J=G TO F: E(J)=E(J+1): NEXT: F=F-1: BEEP 3,1: GOTO 42
38 H=H+1
39 NEXT
40 IF H=F+1 THEN 3000
42 PSET(X,Y): IF P<>P1 OR Q<>Q1 THEN PRESET(P,Q)
43 P1=P: Q1=Q
45 D=STICK(0)
50 IF D=1 OR D=3 OR D=5 OR D=7 THEN A=D
55 IF A<>B THEN C=C+CHR$(X)+CHR$(Y)+CHR$(A)
60 B=A
65 K=K-(K<>20)
70 IF K=20 AND F<9 THEN GOSUB 2000
80 L=L-(M<>0): IF M<>0 THEN BEEP 49,1: LOCATE 13,1: PRINT L;
85 IF PL<L THEN PL=L: N=1: LOCATE 13,3: PRINT PL
86 M=M+(M>0)
90 IF M>0 THEN 120
95 IF LEN(C)=0 THEN 110
100 IF P<>ASC(MID$(C,1,1)) OR Q<>ASC(MID$(C,2,1)) THEN 110
105 I=ASC(MID$(C,3,1)): C=MID$(C,4,LEN(C)-3)
110 P=P+(I=7)-(I=3)
115 Q=Q+(I=1)-(I=5)
120 X=X+(A=7)-(A=3)
125 Y=Y+(A=1)-(A=5)
999 GOTO 20
2000 IF F=9 THEN RETURN
2005 O=RND(1)*43+1: R=RND(1)*29+1
2010 IF POINT(O,R) THEN 2005
2015 F=F+1: E(F)=O+R*256
2020 PSET(O,R)
2025 K=0
2030 RETURN
3000 X=X-(A=7)+(A=3): Y=Y-(A=1)+(A=5): FOR I=1 TO 10: BEEP 47,4: PSET(X,Y): BEEP 48,4
3005 PRESET(X,Y): NEXT
3010 CLS: PRINT"   Vous vous etes     plante ";
3020 IF N THEN PRINT", mais vous detenez le record";: ELSE PRINT,
3030 PRINT" a "L" points";
3050 IF INKEY$<>"J" THEN 3050 ELSE GOTO 9